<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton - http://www.bigbluebutton.org
  
  Copyright (c) 2008-2009 by respective authors (see below). All rights reserved.
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 3 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, If not, see <http://www.gnu.org/licenses/>.

  Authors: Denis Zgonjanin <me.snap@gmail.com>
           Richard Alam <ritzalam@gmail.com> 
  
  $Id: $
--> 

<MDIWindow xmlns="flexlib.mdi.containers.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="400" height="300"
	creationComplete="onCreationComplete()"
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	xmlns:mate="http://mate.asfusion.com/"
	title="Desktop Sharing" 
	showCloseButton="false">
	
	<mate:Listener type="{ViewStreamEvent.STOP}" method="onStopViewStreamEvent" />
		
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.modules.deskShare.events.ViewWindowEvent;
			import org.bigbluebutton.modules.deskShare.events.ViewStreamEvent;
			import org.bigbluebutton.common.Images;
			import flexlib.mdi.events.MDIWindowEvent;
			import mx.core.UIComponent;
			
			private var images:Images = new Images();
			[Bindable] public var fitToWidthIcon:Class = images.magnifier;
			[Bindable] public var fitToActualSizeIcon:Class = images.mag_reset;
			
			private var video:Video;
			private var ns:NetStream;
			private var videoHolder:UIComponent;
			private var stream:String;
			private var videoHeight:Number;
			private var videoWidth:Number;
			
			private var _xPosition:int = 675;
			private var _yPosition:int = 310;
			
			// The following code block is to deal with a bug in FLexLib 
			// with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			
			private static const VIDEO_WIDTH_PADDING:int = 7;
			private static const VIDEO_HEIGHT_PADDING:int = 65;
			
			// Timer to make sure the video fits to window whenever the window is resized.
			private var fitToWindowTimer:Timer;
			
			override public function maximize():void{
				if (!isMaximized){
					savedWindowHeight = this.height;
					savedWindowWidth = this.width;
					savedX = this.x;
					savedY = this.y;    
					dispatchEvent(new MDIWindowEvent(MDIWindowEvent.MAXIMIZE, this));            	
                	isMaximized = true;
                	windowControls.maximizeRestoreBtn.toolTip = "Restore to previous size";
				} else{
					this.width = savedWindowWidth;
					this.height = savedWindowHeight;
					this.x = savedX;
					this.y = savedY;
					isMaximized = false;
					windowControls.maximizeRestoreBtn.toolTip = "Maximize this window";
				}				
			}
			
			private function timerHandler(event:TimerEvent):void {				
				fitToWindow();
			}
			
			private function onCreationComplete():void{
				windowControls.maximizeRestoreBtn.toolTip = "Maximize this window";
				windowControls.minimizeBtn.toolTip = "Minimize this window";
				
				videoHolder = new UIComponent();
				videoHolder.addChild(video);
				this.addChild(videoHolder);
				videoHolder.percentWidth = 100;
				videoHolder.percentHeight = 100;
				this.width = 600;
				addEventListener(MDIWindowEvent.RESIZE_END, onResizeEndEvent);				
				fitToWindow();
				
				/*
				 * Need to have a timer to trigger resizing video to window after the 
				 * window has been maximized/restored. The MAXIMIZE/RESTORE events are
				 * generated when the button is clicked so the width and height are not
				 * the ones we want.
				 */
				fitToWindowTimer = new Timer(100);
				fitToWindowTimer.addEventListener(TimerEvent.TIMER, timerHandler);
				fitToWindowTimer.start();
			}
			
			private function onResizeEndEvent(event:MDIWindowEvent):void {
				if (event.window == this) {
					fitToWindow();
				}
			}
			
			public function startVideo(connection:NetConnection, stream:String, width:Number, height:Number):void{
				this.videoWidth = width;
				this.videoHeight = height;
				
				ns = new NetStream(connection);
				ns.addEventListener( NetStatusEvent.NET_STATUS, onNetStatus );
				ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR, onAsyncError);
				ns.client = this;
				ns.bufferTime = 0;
				ns.receiveVideo(true);
				ns.receiveAudio(false);
				
				video = new Video(width, height);
				video.width = width;
				video.height = height;
				video.attachNetStream(ns);
				ns.play(stream);	
				this.stream = stream;
			}
			
			public function stopViewing():void {
				ns.close();
				closeWindow();				
			}
			
			private function onStopViewStreamEvent(event:ViewStreamEvent):void {
				stopViewing();
			}
						
			private function onAsyncError(e:AsyncErrorEvent):void{
				LogUtil.debug("VIdeoWindow::asyncerror " + e.toString());
			}
			
			private function onNetStatus(e:NetStatusEvent):void{
				switch(e.info.code){
				case "NetStream.Publish.Start":
					LogUtil.debug("NetStream.Publish.Start for broadcast stream " + stream);
					break;
				case "NetStream.Play.UnpublishNotify":
					LogUtil.debug("NetStream.Play.UnpublishNotify for broadcast stream " + stream);
					stopViewing();
					break;
				}
			}		
			
			public function get xPosition():int {
				return _xPosition;
			}
			
			public function get yPosition():int {
				return _yPosition;
			}
			
			public function set xPosition(x:int):void {
				_xPosition = x;
			}
			
			public function set yPosition(y:int):void {
				_yPosition = y;
			}	
			
			private var _defaultWidth:int = 400;
			private var _defaultHeight:int = 300; 
			
			public function get defaultWidth():int{
				return _defaultWidth;
			}
			
			public function get defaultHeight():int{
				return _defaultHeight;
			}
			
			public function set defaultHeight(height:int):void{
				this._defaultHeight = height;
			}
			
			public function set defaultWidth(width:int):void{
				this._defaultWidth = width;
			}
			
			public function resetWidthAndHeight():void{
				this.width = _defaultWidth;
				this.height = _defaultHeight;
			}
												
			/**
			 * resizes the desktop sharing video to fit to this window
			 */
			private function fitToWindow():void{
				if (videoIsSmallerThanWindow()) {
					fitWindowToVideo();
				}
				
				// Ignore if we are displaying the actual size of the video
				if (! btnActualSize.selected) {
					fitVideoToWindow();
				}
			}
			
			private function fitVideoToWindow():void {
				if (this.width < this.height) {
					fitToWidthAndAdjustHeightToMaintainAspectRatio();				
				} else {
					fitToHeightAndAdjustWidthToMaintainAspectRatio();
				}				
			}
						
			private function fitWindowToVideo():void {	
				video.width = videoWidth;
				videoHolder.width = videoWidth;
				video.height = videoHeight;
				videoHolder.height = videoHeight;			
				this.height = videoHeight + VIDEO_HEIGHT_PADDING;
				this.width = videoWidth + VIDEO_WIDTH_PADDING;
			}
			
			private function videoIsSmallerThanWindow():Boolean {
				return (videoHeight < this.height) && (videoWidth < this.width);
			}
			
		
			private function fitToWidthAndAdjustHeightToMaintainAspectRatio():void {
					video.width = this.width - VIDEO_WIDTH_PADDING;
					videoHolder.width = video.width;
					// Maintain aspect-ratio
					video.height = (videoHeight * video.width) / videoWidth;
					videoHolder.height = video.height;
					this.height = video.height + VIDEO_HEIGHT_PADDING;					
			}
				
			private function fitToHeightAndAdjustWidthToMaintainAspectRatio():void {
					video.height = this.height - VIDEO_HEIGHT_PADDING;
					videoHolder.height = video.height;
					// Maintain aspect-ratio
					video.width = (videoWidth * video.height) / videoHeight;
					videoHolder.width = video.width;
					this.width = video.width + VIDEO_WIDTH_PADDING;					
			}
								
			/**
			 * resizes the desktop sharing video to actual video resolution
			 */
			private function fitToActualSize():void{
				if (videoIsSmallerThanWindow()) {
					fitWindowToVideo();
				} else {
					video.width = videoWidth;
					videoHolder.width = videoWidth;
					video.height = videoHeight;
					videoHolder.height = videoHeight;					
				}
			}
			
			private function determineHowToDisplayVideo():void {
				if (btnActualSize.selected) {
					fitToActualSize();			
					btnActualSize.toolTip = "Fit to window";	
				} else {
					fitToWindow();
					btnActualSize.toolTip = "Display actual size";
				}
			}
			
			private function closeWindow():void {
				dispatchEvent(new ViewWindowEvent(ViewWindowEvent.CLOSE));
			}
		]]>
	</mx:Script>
	
	<mx:ControlBar id="bottomBar" height="35">
		<mx:Button id="btnActualSize" toggle="true" click="determineHowToDisplayVideo()" 
			height="90%" icon="{btnActualSize.selected ? fitToActualSizeIcon : fitToWidthIcon}" 
			toolTip="Display actual size" />
	</mx:ControlBar>
</MDIWindow>
