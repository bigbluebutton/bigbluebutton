<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
--> 

<mdi:CustomMdiWindow xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:mdi="org.bigbluebutton.common.*" 
	xmlns:flc="flexlib.controls.*"
	implements="org.bigbluebutton.common.IBbbModuleWindow" 
	xmlns:mate="http://mate.asfusion.com/"
	creationComplete="onCreationComplete()"
	title="{ResourceUtil.getInstance().getString('bbb.users.title')}"
	showCloseButton="false">

	<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />
	<mate:Listener type="{ShortcutEvent.RAISE_HAND}" method="remoteRaiseHand" />
	<mate:Listener type="{ShortcutEvent.FOCUS_USERS_WINDOW}" method="focusWindow" />
	<mate:Listener type="{ShortcutEvent.MUTE_ALL_BUT_PRES}" method="remoteMuteAllButPres" />
	<mate:Listener type="{MeetingMutedEvent.MEETING_MUTED}" method="handleMeetingMuted" />
	<mate:Listener type="{LockControlEvent.CHANGED_LOCK_SETTINGS}" method="handleChangedLockSettingsEvent" />
	<mate:Listener type="{ChangeMyRole.CHANGE_MY_ROLE_EVENT}" method="onChangeMyRole" />
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.controls.Menu;
			import mx.controls.listClasses.IListItemRenderer;
			import mx.core.IFlexDisplayObject;
			import mx.events.ListEvent;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.common.Role;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.core.UsersUtil;
			import org.bigbluebutton.core.events.LockControlEvent;
			import org.bigbluebutton.core.events.VoiceConfEvent;
			import org.bigbluebutton.core.managers.UserManager;
			import org.bigbluebutton.core.model.MeetingModel;
			import org.bigbluebutton.core.vo.LockSettingsVO;
			import org.bigbluebutton.main.events.ShortcutEvent;
			import org.bigbluebutton.main.model.users.BBBUser;
			import org.bigbluebutton.main.model.users.events.ChangeMyRole;
			import org.bigbluebutton.main.model.users.events.ChangeRoleEvent;
			import org.bigbluebutton.main.model.users.events.ChangeStatusEvent;
			import org.bigbluebutton.main.model.users.events.KickUserEvent;
			import org.bigbluebutton.main.model.users.events.RoleChangeEvent;
			import org.bigbluebutton.main.views.LockSettings;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.users.events.MeetingMutedEvent;
			import org.bigbluebutton.modules.users.events.UsersEvent;
			import org.bigbluebutton.modules.users.events.UsersRollEvent;
			import org.bigbluebutton.modules.users.model.UsersOptions;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
			private var dispatcher:Dispatcher;
			
			private var keyCombos:Object;
			private var modifier:String;
			
			[Bindable]
			private var roomMuted:Boolean = false;
			
			[Bindable]
			private var roomLocked:Boolean = false;
			
			private var myMenu:Menu;
			
			[Bindable]
			public var partOptions:UsersOptions;
			
			[Bindable]
			private var images:Images = new Images();
			
			[Bindable]
			private var users:ArrayCollection = new ArrayCollection();
			
			private var amIModerator:Boolean = false;
			
			private const FOCUS_USERS_LIST:String = "Focus Users List";
			private const MAKE_PRESENTER:String = "Make Presenter";
			private const KICK_USER:String = "Kick User";
			private const MUTE_USER:String = "Mute User";
			private const MUTE_ALL_USER:String = "Mute All User";
			
			private var muteMeRolled:Boolean = false;
			
			private var moodMenuHeight:Number = -1;
			
			private function onChangeMyRole(e:ChangeMyRole):void {
				refreshRole(e.role == Role.MODERATOR);
			}
			
			private function onCreationComplete():void {
				dispatcher = new Dispatcher();
			
				users = UserManager.getInstance().getConference().users;
				refreshRole(UserManager.getInstance().getConference().amIModerator());
				
				BindingUtils.bindSetter(updateNumberofUsers, users, "length");
				BindingUtils.bindSetter(onMoodChanged, UsersUtil.getMyself(), "mood");
				
				this.addEventListener(KeyboardEvent.KEY_DOWN, handleKeyDown);
				ResourceUtil.getInstance().addEventListener(Event.CHANGE, localeChanged); // Listen for locale changing
				
				modifier = ExternalInterface.call("determineModifier");
				loadKeyCombos(modifier);
				
				resourcesChanged();
				
				roomMuted = MeetingModel.getInstance().meetingMuted;
				var lockSettings:LockSettingsVO = UserManager.getInstance().getConference().getLockSettings();
				roomLocked = lockSettings.isAnythingLocked() && ( lockSettings.getLockOnJoin() || UsersUtil.isAnyoneLocked());
				
				titleBarOverlay.tabIndex = partOptions.baseTabIndex;
				minimizeBtn.tabIndex = partOptions.baseTabIndex+1;
				maximizeRestoreBtn.tabIndex = partOptions.baseTabIndex+2;
				closeBtn.tabIndex = partOptions.baseTabIndex+3;
				
				stage.addEventListener(MouseEvent.MOUSE_OVER, handleStageMouseOver);
				usersGrid.addEventListener(MouseEvent.MOUSE_OVER, handleUsersGridMouseOver);
				
				addContextMenuItems();
			}
			
			private function refreshRole(moderator:Boolean = true):void {
				amIModerator = moderator;

				settingsBtn.visible = settingsBtn.includeInLayout = partOptions.enableSettingsButton && amIModerator;

				changeButtons(UserManager.getInstance().getConference().amIPresenter);

				if (myMenu) myMenu.hide();
			}
			
			public function getPrefferedPosition():String{
				return MainCanvas.TOP_LEFT;
			}
			
			private function addContextMenuItems():void {
				var contextMenuItems:Array = new Array();
				
				var exportUsersItem:ContextMenuItem = new ContextMenuItem(ResourceUtil.getInstance().getString("bbb.users.usersGrid.contextmenu.exportusers"));
				exportUsersItem.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, exportUsersItemHandler);
				contextMenuItems.push(exportUsersItem);
				
				this.customContextMenuItems = contextMenuItems;
			}
			
			private function exportUsersItemHandler(e:ContextMenuEvent):void{
				if (e.target.caption == ResourceUtil.getInstance().getString("bbb.users.usersGrid.contextmenu.exportusers")) {
					var userNames:String = "";
					var returnStr:String = (Capabilities.os.indexOf("Windows") >= 0 ? "\r\n" : "\r");
					for (var i:int = 0; i < users.length; i++) {
						userNames += users[i].name + returnStr;
					}
					System.setClipboard(userNames);
				}
			}
			
			private function updateNumberofUsers(numUsers:int):void {
				if (numUsers > 8)
					this.title = ResourceUtil.getInstance().getString('bbb.users.title', [":", numUsers]);
				else 
					this.title = ResourceUtil.getInstance().getString('bbb.users.title', ["", ""]);
			}
			
			private function localeChanged(e:Event):void {
				updateNumberofUsers(users.length);
				resourcesChanged();
			}
			
			private function changeButtons(presenter:Boolean):void {
				raiseHandBtn.visible = raiseHandBtn.includeInLayout = partOptions.enableRaiseHand;
			}
			
			/*
			 * Work around for a bug with the users grid. When you click on one of the buttons in an item renderer the client 
			 * locks up briefly and any mouse movements while the client is locked up are ignored. This means that roll outs 
			 * aren't fired if the mouse is moved quickly while the client is updating. This block of code watches for 
			 * anything outside of the usersGrid being rolled over and notifies the item renders that they should reset.
			 * 
			 *  - Chad
			 */
			private var rolledOver:Boolean = false;
			private var o:Object;
			
			private function handleUsersGridMouseOver(e:MouseEvent):void {
				rolledOver = true;
				o = e.target;
			}
			
			private function handleStageMouseOver(e:MouseEvent):void {
				if (rolledOver && o != e.target) {
					rolledOver = false;
					o = null;
					var rollEvent:UsersRollEvent = new UsersRollEvent(UsersRollEvent.USER_ROLL_OUT, null);
					dispatcher.dispatchEvent(rollEvent);
				}
			}
			
			/*
			 * End of the work around.
			 */
			
			private function onItemRollOver(event:ListEvent):void{
				var item:IListItemRenderer = event.itemRenderer;
				var user:BBBUser = item.data as BBBUser;
				var rollEvent:UsersRollEvent = new UsersRollEvent(UsersRollEvent.USER_ROLL_OVER, user.userID);
				dispatcher.dispatchEvent(rollEvent);
			}
			
			private function onItemRollOut(event:ListEvent):void{
				var item:IListItemRenderer = event.itemRenderer;
				var user:BBBUser = item.data as BBBUser;
				var rollEvent:UsersRollEvent = new UsersRollEvent(UsersRollEvent.USER_ROLL_OUT, user.userID);
				dispatcher.dispatchEvent(rollEvent);
			}
			
			private function raiseHand():void{
				var e:ChangeStatusEvent;
				var userID:String = UserManager.getInstance().getConference().getMyUserId();
				if (UserManager.getInstance().getConference().getMyMood() == ChangeStatusEvent.RAISE_HAND) {
					e = new ChangeStatusEvent(userID, ChangeStatusEvent.CLEAR_STATUS);
					raiseHandBtn.accessibilityName = getStringForStatus(ChangeStatusEvent.CLEAR_STATUS);
				} else {
					e = new ChangeStatusEvent(userID, ChangeStatusEvent.RAISE_HAND);
					raiseHandBtn.accessibilityName = getStringForStatus(ChangeStatusEvent.RAISE_HAND);
				}
				dispatchEvent(e);
			}

			private function clearAllStatus():void {
				for (var i:int = 0; i < users.length; i++) {
					var p:BBBUser = users.getItemAt(i) as BBBUser;
					if (p.hasMood) {
						dispatchEvent( new ChangeStatusEvent(p.userID, ChangeStatusEvent.CLEAR_STATUS) );
					}
				}
			}
			
			private function getStringForStatus(status:String):String {
				switch (status) {
					case ChangeStatusEvent.CLEAR_STATUS:
						return ResourceUtil.getInstance().getString('bbb.users.status.clearStatus');
					case ChangeStatusEvent.RAISE_HAND:
						return ResourceUtil.getInstance().getString('bbb.users.status.raiseHand');
					case ChangeStatusEvent.AGREE:
						return ResourceUtil.getInstance().getString('bbb.users.status.agree');
					case ChangeStatusEvent.DISAGREE:
						return ResourceUtil.getInstance().getString('bbb.users.status.disagree');
					case ChangeStatusEvent.SPEAK_LOUDER:
						return ResourceUtil.getInstance().getString('bbb.users.status.speakLouder');
					case ChangeStatusEvent.SPEAK_LOWER:
						return ResourceUtil.getInstance().getString('bbb.users.status.speakSofter');
					case ChangeStatusEvent.SPEAK_FASTER:
						return ResourceUtil.getInstance().getString('bbb.users.status.speakFaster');
					case ChangeStatusEvent.SPEAK_SLOWER:
						return ResourceUtil.getInstance().getString('bbb.users.status.speakSlower');
					case ChangeStatusEvent.BE_RIGHT_BACK:
						return ResourceUtil.getInstance().getString('bbb.users.status.beRightBack');
					case ChangeStatusEvent.LAUGHTER:
						return ResourceUtil.getInstance().getString('bbb.users.status.laughter');
					case ChangeStatusEvent.SAD:
						return ResourceUtil.getInstance().getString('bbb.users.status.sad');
					default:
						return ResourceUtil.getInstance().getString('bbb.users.raiseHandBtn.toolTip');
				}
			}

			private function openMoodMenu():void {
				var myMenuData:Array = [];
				if(partOptions.enableRaiseHand) {
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.CLEAR_STATUS), icon: images.mood_clear, handler: function():void { changeStatus(ChangeStatusEvent.CLEAR_STATUS); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.RAISE_HAND), icon: images.mood_raise_hand, handler: function():void { changeStatus(ChangeStatusEvent.RAISE_HAND); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.AGREE), icon: images.mood_agreed, handler: function():void { changeStatus(ChangeStatusEvent.AGREE); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.DISAGREE), icon: images.mood_disagreed, handler: function():void { changeStatus(ChangeStatusEvent.DISAGREE); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.SPEAK_LOUDER), icon: images.mood_speak_louder, handler: function():void { changeStatus(ChangeStatusEvent.SPEAK_LOUDER); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.SPEAK_LOWER), icon: images.mood_speak_softer, handler: function():void { changeStatus(ChangeStatusEvent.SPEAK_LOWER); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.SPEAK_FASTER), icon: images.mood_speak_faster, handler: function():void { changeStatus(ChangeStatusEvent.SPEAK_FASTER); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.SPEAK_SLOWER), icon: images.mood_speak_slower, handler: function():void { changeStatus(ChangeStatusEvent.SPEAK_SLOWER); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.BE_RIGHT_BACK), icon: images.mood_be_right_back, handler: function():void { changeStatus(ChangeStatusEvent.BE_RIGHT_BACK); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.LAUGHTER), icon: images.mood_happy, handler: function():void { changeStatus(ChangeStatusEvent.LAUGHTER); }} );
					myMenuData.push( {label: getStringForStatus(ChangeStatusEvent.SAD), icon: images.mood_sad, handler: function():void { changeStatus(ChangeStatusEvent.SAD); }} );
				}
				
				// make sure the previous menu is closed before opening a new one
				// This could be improved to include a flag that tells if the menu is open,
				// but it would require an extra listener for the MenuCloseEvent.
				if (myMenu) myMenu.hide();

				myMenu = Menu.createMenu(null, myMenuData, true);

				if (moodMenuHeight == -1) {
					myMenuData.length * myMenu.rowHeight;
				}

				var raiseHandBtnPos:Point = raiseHandBtn.localToGlobal(new Point(0,0));

				var myMenuPos:Point = new Point();
				myMenuPos.x = raiseHandBtnPos.x + 1;
				myMenuPos.y = raiseHandBtnPos.y + raiseHandBtn.height + 1;
				if (myMenuPos.y + moodMenuHeight > stage.stageHeight) {
					myMenuPos.x += raiseHandBtn.width;
					myMenuPos.y = stage.stageHeight - moodMenuHeight;
				}
				
				myMenu.addEventListener(MenuEvent.ITEM_CLICK, menuClickHandler);
				myMenu.addEventListener(MenuEvent.MENU_SHOW, moodMenuShowHandler);
				myMenu.show(myMenuPos.x, myMenuPos.y);
			}
			
			private function moodMenuShowHandler(e:MenuEvent):void {
				var menu:Menu = e.menu as Menu;

				// TODO it doesn't work because menu.y is always 0
				if (menu.y + menu.height > stage.stageHeight) {
					var myMenuPos:Point = new Point();
					myMenuPos.x = menu.x + raiseHandBtn.width;
					myMenuPos.y = stage.stageHeight - menu.height;
					menu.show(myMenuPos.x, myMenuPos.y);
				}

				moodMenuHeight = menu.height;
				myMenu.setFocus();
			}

			private function openPresenterSettings():void {
				var myMenuData:Array = [];
				myMenuData.push({label: ResourceUtil.getInstance().getString('bbb.users.settings.clearAllStatus'), icon: images.mood_clear, handler:clearAllStatus});
				
				if (!roomMuted) {
					myMenuData.push({label: ResourceUtil.getInstance().getString('bbb.users.settings.muteAll'), icon: images.audio_muted, handler: muteAll});
					if (UserManager.getInstance().getConference().getPresenter())
						myMenuData.push({label: ResourceUtil.getInstance().getString('bbb.users.settings.muteAllExcept') + ": " + UserManager.getInstance().getConference().getPresenter().name, icon: images.audio_muted, handler:muteAlmostAll});
				} else
					myMenuData.push({label: ResourceUtil.getInstance().getString('bbb.users.settings.unmuteAll'), icon: images.audio, handler:muteAll});
								
				myMenuData.push({label: ResourceUtil.getInstance().getString('bbb.users.settings.lockSettings'), icon: images.lock_open, handler: lockSettings});
				
				
				// make sure the previous menu is closed before opening a new one
				// This could be improved to include a flag that tells if the menu is open,
				// but it would require an extra listener for the MenuCloseEvent.
				if (myMenu) myMenu.hide();
				
				myMenu = Menu.createMenu(null, myMenuData, true);
				
				myMenu.variableRowHeight = true;
				myMenu.show(this.x + settingsBtn.x + settingsBtn.width + 2, this.y + this.height);
				myMenu.addEventListener(MenuEvent.ITEM_CLICK, menuClickHandler);
				myMenu.addEventListener(MenuEvent.MENU_SHOW, presenterMenuShowHandler);
			}
			
			private function menuClickHandler(e:MenuEvent):void {
				e.item.handler();
			}
			
			private function presenterMenuShowHandler(e:MenuEvent):void {
				myMenu.setFocus();
			}
			
			private function handleChangedLockSettingsEvent(e:LockControlEvent):void {
				var lockSettings:LockSettingsVO = UserManager.getInstance().getConference().getLockSettings();
				roomLocked = lockSettings.isAnythingLocked() && ( lockSettings.getLockOnJoin() || UsersUtil.isAnyoneLocked());
			}
			
			private function changeStatus(status:String):void {
				var e:ChangeStatusEvent = new ChangeStatusEvent(UserManager.getInstance().getConference().getMyUserId(), status);
				dispatchEvent(e);
			}
			
			private function lockSettings():void {
				LogUtil.traceObject("Action: lockSettings");
				
				var event:LockControlEvent = new LockControlEvent(LockControlEvent.OPEN_LOCK_SETTINGS);
				dispatcher.dispatchEvent(event);
			}
			
			private function handleMeetingMuted(e:MeetingMutedEvent):void {
				roomMuted = MeetingModel.getInstance().meetingMuted;
			}
			
			private function muteAll():void {
				if (amIModerator) {
					if (!roomMuted) {
						var muteCommand:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.MUTE_ALL);
						dispatchEvent(muteCommand);
						roomMuted = true;
					} else {
						var unmuteCommand:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.UNMUTE_ALL);
						dispatchEvent(unmuteCommand);
						roomMuted = false;
					}
				}
			}
			
			private function muteAlmostAll():void {
				if (amIModerator) {
					if (!roomMuted) {
						var muteCommand:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.MUTE_ALMOST_ALL);
						dispatchEvent(muteCommand);
						roomMuted = true;
					}
				}
			}
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();				
				if (users.length > 8)
					this.title = ResourceUtil.getInstance().getString('bbb.users.title', [":", users.length]);
				else 
					this.title = ResourceUtil.getInstance().getString('bbb.users.title', ["", ""]);
				
				if (titleBarOverlay != null) {
					titleBarOverlay.accessibilityName = ResourceUtil.getInstance().getString('bbb.users.titleBar');
				}
				
				if (windowControls != null) {
					minimizeBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.minimizeBtn.toolTip");
					minimizeBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.users.minimizeBtn.accessibilityName");
					
					maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.maximizeRestoreBtn.toolTip");
					maximizeRestoreBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.users.maximizeRestoreBtn.accessibilityName");
				}
				
				if (raiseHandBtn != null) {
					raiseHandBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.users.raiseHandBtn.accessibilityName");
				}
				
				addContextMenuItems();
			}
			
			private function loadKeyCombos(modifier:String):void {
				keyCombos = new Object(); // always start with a fresh array bbb.shortcutkey.users.muteall
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.users.focusUsers') as String)] = FOCUS_USERS_LIST;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.users.makePresenter') as String)] = MAKE_PRESENTER;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.users.kick') as String)] = KICK_USER;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.users.mute') as String)] = MUTE_USER;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.users.muteall') as String)] = MUTE_ALL_USER;
				//TODO Include shortcuts to lock control
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.general.maximize') as String)] = ShortcutEvent.MAXIMIZE_USERS;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.general.minimize') as String)] = ShortcutEvent.MINIMIZE_USERS;
			}
			
			// Handle general-access hotkeys, regardless of what window the user is focused in
			private function handleKeyDown(e:KeyboardEvent):void {
				if (keyCombos == null) loadKeyCombos(modifier);
				var keyPress:String = (e.ctrlKey  ? "control+" : "") + (e.shiftKey ? "shift+"   : "") +
					(e.altKey   ? "alt+"     : "") + e.keyCode;		                              
				if (keyCombos[keyPress]) {
					switch(keyCombos[keyPress]) {
						case FOCUS_USERS_LIST:
							remoteFocusUsers();
							break;
						case MAKE_PRESENTER:
							remoteMakePresenter();
							break;
						case KICK_USER:
							remoteKickUser();
							break;
						case MUTE_USER:
							remoteMuteUser();
							break;
						case MUTE_ALL_USER:
							muteAll();
							break;
						case ShortcutEvent.MAXIMIZE_USERS:
							remoteMaximize();
							break;
						case ShortcutEvent.MINIMIZE_USERS:
							remoteMinimize();
							break;
					}
				}
			}
			
			private function focusWindow(e:ShortcutEvent):void{
				focusManager.setFocus(titleBarOverlay);
			}
			
			public function remoteRaiseHand(e:ShortcutEvent):void{
				raiseHand();
			}
			
			private function remoteMinimize():void{
				if (!minimized){
					this.minimize();
				}
			}
			
			private function remoteMaximize():void{
				if (!maximized && !minimized){
					this.maximize();
				} else{
					this.restore();
				}				
			}
			
			public function remoteMakePresenter():void{
				if (amIModerator && usersGrid.selectedIndex != -1) {
					var selData:Object = usersGrid.selectedItem;
					
					if (!selData.presenter && !selData.phoneUser) {
						var e:RoleChangeEvent = new RoleChangeEvent(RoleChangeEvent.ASSIGN_PRESENTER);
						e.userid = selData.userID;
						e.username = selData.name;
						dispatchEvent(e);
					}
				}
			}
			
			public function remoteKickUser():void{
				if (amIModerator && usersGrid.selectedIndex != -1 && partOptions.allowKickUser) {
					var selData:Object = usersGrid.selectedItem;
					
					if (!selData.me)
						dispatchEvent(new KickUserEvent(selData.userID));
				}
			}
			
			public function remoteMuteUser():void{
				if (amIModerator && usersGrid.selectedIndex != -1) {
					var selData:Object = usersGrid.selectedItem;
					
					if (selData.voiceJoined) {
						var e:VoiceConfEvent = new VoiceConfEvent(VoiceConfEvent.MUTE_USER);
						e.userid = selData.voiceUserid;
						e.mute = !selData.voiceMuted;
						dispatchEvent(e);
					}
				}
			}
			
			public function remoteFocusUsers():void{
				focusManager.setFocus(usersGrid);
				usersGrid.drawFocus(true);
			}
			
			private function remoteMuteAllButPres(e:ShortcutEvent):void{
				muteAlmostAll();
			}
		
			private function onMoodChanged(mood:String):void{
				var icon:Class = null;
				switch(mood) {
					case ChangeStatusEvent.RAISE_HAND:
						icon = images.mood_raise_hand;
						break;
					case ChangeStatusEvent.AGREE:
						icon = images.mood_agreed;
						break;
					case ChangeStatusEvent.DISAGREE:
						icon = images.mood_disagreed;
						break;
					case ChangeStatusEvent.SPEAK_LOUDER:
						icon = images.mood_speak_louder;
						break;
					case ChangeStatusEvent.SPEAK_LOWER:
						icon = images.mood_speak_softer;
						break;
					case ChangeStatusEvent.SPEAK_FASTER:
						icon = images.mood_speak_faster;
						break;
					case ChangeStatusEvent.SPEAK_SLOWER:
						icon = images.mood_speak_slower;
						break;
					case ChangeStatusEvent.BE_RIGHT_BACK:
						icon = images.mood_be_right_back;
						break;
					case ChangeStatusEvent.LAUGHTER:
						icon = images.mood_happy;
						break;
					case ChangeStatusEvent.SAD:
						icon = images.mood_sad;
						break;
					default:
						icon = images.mood;
						break;
				}
				
				raiseHandBtn.setStyle("icon", icon);
				if (mood == ChangeStatusEvent.CLEAR_STATUS) {
					raiseHandBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.users.raiseHandBtn.accessibilityName");
				} else {
					raiseHandBtn.accessibilityName = getStringForStatus(mood);
				}
			}	
			
		]]>
	</mx:Script>
	
	<mx:DataGrid id="usersGrid" dataProvider="{users}" editable="false" sortableColumns="true"
    	dragEnabled="false" width="100%" height="100%" draggableColumns="false"
    	itemRollOver="onItemRollOver(event)"
		itemRollOut="onItemRollOut(event)" 
		tabIndex="{partOptions.baseTabIndex+5}"
		accessibilityName="{ResourceUtil.getInstance().getString('bbb.users.usersGrid.accessibilityName')}" >
    	<mx:columns>
    		<mx:DataGridColumn dataField="userStatus" headerText="{ResourceUtil.getInstance().getString('bbb.users.usersGrid.statusItemRenderer')}" editable="false" width="45" minWidth="45"
    			itemRenderer="org.bigbluebutton.modules.users.views.StatusItemRenderer" sortable="false" />
    		<mx:DataGridColumn dataField="name" headerText="{ResourceUtil.getInstance().getString('bbb.users.usersGrid.nameItemRenderer')}" editable="false" sortable="false" minWidth="60"
    			itemRenderer="org.bigbluebutton.modules.users.views.NameItemRenderer"/>
    		<mx:DataGridColumn dataField="media" headerText="{ResourceUtil.getInstance().getString('bbb.users.usersGrid.mediaItemRenderer')}" sortable="false" width="110" minWidth="110"
    			itemRenderer="org.bigbluebutton.modules.users.views.MediaItemRenderer"/>
    	</mx:columns>
    </mx:DataGrid>
	
	<mx:ControlBar width="100%">
		<mx:Button id="raiseHandBtn" icon="{images.mood}" selected="{UserManager.getInstance().getConference().isMyHandRaised}"
				   width="30" height="30" toolTip="{ResourceUtil.getInstance().getString('bbb.users.raiseHandBtn.toolTip')}" click="openMoodMenu()"
				   visible="true" tabIndex="{partOptions.baseTabIndex+10}" />
		<mx:Button id="settingsBtn" icon="{images.users_settings}" width="30" height="30"
					toolTip="{ResourceUtil.getInstance().getString('bbb.users.settings.buttonTooltip')}" click="openPresenterSettings()" visible="true" tabIndex="{partOptions.baseTabIndex+15}" />
		<mx:VBox>
			<mx:Label text="{ResourceUtil.getInstance().getString('bbb.users.roomMuted.text')}" visible="{roomMuted}" includeInLayout="{roomMuted}" />
			<mx:Label text="{ResourceUtil.getInstance().getString('bbb.users.roomLocked.text')}" visible="{roomLocked}" includeInLayout="{roomLocked}" />
		</mx:VBox>
	</mx:ControlBar>
	
</mdi:CustomMdiWindow>
