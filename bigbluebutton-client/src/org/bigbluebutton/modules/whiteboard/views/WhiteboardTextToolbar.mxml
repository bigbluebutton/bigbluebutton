<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<mx:HBox xmlns="flexlib.containers.*"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:mate="http://mate.asfusion.com/"
		 xmlns:views="org.bigbluebutton.modules.whiteboard.views.*"
		 implements="org.bigbluebutton.modules.whiteboard.views.IWhiteboardToolbar"
		 visible="false">
	
	<fx:Declarations>
		<mate:Listener type="{WhiteboardButtonEvent.DISABLE_WHITEBOARD}" method="disableTextToolbar" />
		<mate:Listener type="{StopWhiteboardModuleEvent.STOP_HIGHLIGHTER_MODULE_EVENT}" method="closeToolbar" />
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.events.MoveEvent;
			import mx.events.ResizeEvent;
			
			import flexlib.mdi.containers.MDIWindow;
			
			import org.as3commons.logging.api.ILogger;
			import org.as3commons.logging.api.getClassLogger;
			import org.bigbluebutton.modules.whiteboard.business.shapes.TextObject;
			import org.bigbluebutton.modules.whiteboard.events.StopWhiteboardModuleEvent;
			import org.bigbluebutton.modules.whiteboard.events.WhiteboardButtonEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
			private static const LOGGER:ILogger = getClassLogger(WhiteboardTextToolbar);      
			public var canvas:WhiteboardCanvas;
			public var textColor:uint;
			public var bgColor:uint;
			public var textSize:Number;
			public var backgroundVisible:Boolean;
			
			private var currentlySelectedTextObject:TextObject = null;	
			private var containerToOverlay:MDIWindow;
			private var hasFocus:Boolean;
			[Bindable] private var fontSizes:Array = ["12", "14", "16", "18", "22", "24", "32", "36"];
			
			private function onFontSizeChange():void {
				setTextSize(Number(textSizeMenu.selectedItem));
			}
			
			/**
			 * Sets the text color of the selected text object
			 * @param e The event
			 * 
			 */		
			protected function onFontColorChange(e:Event):void{
				setTextColor(e.target.selectedColor);
			}
			
			/**
			 * Sets the text size of the selected text object
			 * @param size the size to set for the selected text object
			 * 
			 */			
			protected function setTextSize(size:Number):void {
				enableTextToolbar();
				this.textSize = size;
				currentlySelectedTextObject.applyNewFormat(textColor, textSize);
			}
			
			protected function setTextColor(color:uint):void {
				enableTextToolbar();
				this.textColor = color;
				currentlySelectedTextObject.applyNewFormat(textColor, textSize);
			}

			public function presenterChange(amIPresenter:Boolean):void{
				if (!amIPresenter) {
					disableTextToolbar();
				}
			}

			private function enableTextToolbar():void{
				this.visible = true;
				callLater(repositionToolbarByWindow);
			}
			
			private function disableTextToolbar(e:Event=null):void{
				this.visible = false;
				closeMenus()
			}
			
			private function closeToolbar(e:StopWhiteboardModuleEvent):void{
				closeMenus();
				if (parent) {
					parent.removeChild(this);
				}
			}
			
			private function closeMenus() : void {
				if (ctextpik) ctextpik.close();
				if (textSizeMenu) textSizeMenu.close();
			}
			
			public function syncPropsWith(tobj:TextObject):void {
				currentlySelectedTextObject = tobj;
				
				if(tobj != null) {
					enableTextToolbar();
					repositionToolbar();
					setTextColor(ctextpik.selectedColor);
					setTextSize(Number(textSizeMenu.selectedItem));
				} else {
					disableTextToolbar();
				}
			}
			
			// invoked by WhiteboardManager when the TextToolbar is first loaded
			public function positionToolbar(container:MDIWindow):void{
				containerToOverlay = container;
				containerToOverlay.addEventListener(ResizeEvent.RESIZE, repositionToolbarByWindow);
				containerToOverlay.addEventListener(MouseEvent.CLICK, repositionToolbarByWindow);
				containerToOverlay.addEventListener(MoveEvent.MOVE, repositionToolbarByWindow);
			}
			
			/* required for repositioning the TextToolbar when an event occurs on the presentation window */
			public function repositionToolbarByWindow(event:Event = null):void {
				repositionToolbar();
			}
			
			public function repositionToolbar():void {
				if (this.visible && currentlySelectedTextObject) {
					// translate TextObject's coords to stage coords because TextToolbar is added to stage
 					
					var loc:Point = canvas.localToGlobal(new Point(currentlySelectedTextObject.x, currentlySelectedTextObject.y));
					this.x = loc.x;
					this.y = loc.y - this.height - 45;
					parent.setChildIndex(this, parent.numChildren-1);
				}
			}
			
			// repositions the toolbar when canvas is zoomed			
			public function adjustForZoom(x:Number, y:Number):void {
				repositionToolbar();
			}
			
		]]>
	</fx:Script>

	<mx:ColorPicker width="20" height="20"  change="onFontColorChange(event)" id="ctextpik" selectedColor="0x000000"
					toolTip="{ResourceUtil.getInstance().getString('ltbcustom.bbb.highlighter.texttoolbar.textColorPicker')}"/>

	<views:CustomComboBox id="textSizeMenu" dataProvider="{fontSizes}" selectedItem="18" change="onFontSizeChange()" rowCount="8"
				 toolTip="{ResourceUtil.getInstance().getString('ltbcustom.bbb.highlighter.texttoolbar.textSizeMenu')}" />
</mx:HBox>
