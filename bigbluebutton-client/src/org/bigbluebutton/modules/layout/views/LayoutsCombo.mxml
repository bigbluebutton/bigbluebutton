<?xml version="1.0" encoding="utf-8"?>
<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml" 
			xmlns:mate="http://mate.asfusion.com/"
			toolTip="{ResourceUtil.getInstance().getString('bbb.layout.combo.toolTip')}"
			prompt="{ResourceUtil.getInstance().getString('bbb.layout.combo.prompt')}"
			height="{LayoutButton.BUTTON_SIZE}" creationComplete="init()"
			change="onSelectedItemChanged(event)"
			disabledColor="{getStyle('color')}" rowCount="10"
			styleName="languageSelectorStyle" >
	
	<mate:Listener type="{SwitchedLayoutEvent.SWITCHED_LAYOUT_EVENT}" method="onLayoutChanged" />
	<mate:Listener type="{LayoutsReadyEvent.LAYOUTS_READY}" method="populateLayoutsList"/>
  <mate:Listener type="{LockControlEvent.CHANGED_LOCK_SETTINGS}" method="lockSettingsChanged" />
	<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />
  
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import flash.events.Event;
			import flash.events.IOErrorEvent;
			import flash.events.ProgressEvent;
			import flash.net.FileReference;
			
			import flexlib.mdi.containers.MDICanvas;
			import flexlib.mdi.containers.MDIWindow;
			import flexlib.mdi.events.MDIManagerEvent;
			import flexlib.mdi.events.MDIWindowEvent;
			import flexlib.mdi.managers.MDIManager;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.ResizeEvent;
			
			import org.bigbluebutton.common.IBbbModuleWindow;
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.common.events.OpenWindowEvent;
			import org.bigbluebutton.core.UsersUtil;
			import org.bigbluebutton.core.events.LockControlEvent;
			import org.bigbluebutton.core.events.SwitchedLayoutEvent;
			import org.bigbluebutton.core.managers.UserManager;
			import org.bigbluebutton.core.vo.LockSettingsVO;
			import org.bigbluebutton.main.views.MainToolbar;
			import org.bigbluebutton.modules.layout.events.ChangeLayoutEvent;
			import org.bigbluebutton.modules.layout.events.ComboBoxCreatedEvent;
			import org.bigbluebutton.modules.layout.events.LayoutChangedEvent;
			import org.bigbluebutton.modules.layout.events.LayoutEvent;
			import org.bigbluebutton.modules.layout.events.LayoutsLoadedEvent;
			import org.bigbluebutton.modules.layout.events.LayoutsReadyEvent;
			import org.bigbluebutton.modules.layout.events.ViewInitializedEvent;
			import org.bigbluebutton.modules.layout.model.LayoutDefinition;
			import org.bigbluebutton.modules.layout.model.LayoutDefinitionFile;
			import org.bigbluebutton.modules.layout.model.LayoutModel;
			import org.bigbluebutton.modules.layout.model.WindowLayout;
			import org.bigbluebutton.modules.layout.views.LayoutButton;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
      private static const LOG:String = "Layout::LayoutsCombo - ";
      
			private var _dispatcher:Dispatcher = new Dispatcher();
      [Bindable] private var layoutNames:ArrayCollection = new ArrayCollection();
      
      private function init():void {
        trace(LOG + "initing");
        dataProvider = layoutNames;
        populateComboBox();
      }
      
      private function lockSettingsChanged(e:LockControlEvent):void {
        var lockSettings:LockSettingsVO = UserManager.getInstance().getConference().getLockSettings();
        if (UsersUtil.amIModerator() || UsersUtil.amIPresenter()) return;
        
        this.enabled = ! lockSettings.getLockedLayout();
      }
      
			private function populateLayoutsList(e:LayoutsReadyEvent):void {
        trace(LOG + " handling layout ready event.");       
        populateComboBox();
			}
      
      private function populateComboBox():void {
        trace(LOG + " populating layout combo.");  
        layoutNames = new ArrayCollection();         
        var layouts:Array = LayoutModel.getInstance().getLayoutNames();
        
        var idx:int = 0, currentLayoutIndex:int = -1;
        for each (var lay:Object in layouts) {
          var translatedName:String = ResourceUtil.getInstance().getString(lay.name)
          if (translatedName == "undefined") translatedName = lay.name;
          var item:Object = {index: idx, label: translatedName, localeKey: lay.name, currentLayout: lay.currentLayout };
          trace(LOG + " layout [" + lay.name + ", current=" + lay.currentLayout + "]");
          layoutNames.addItem(item);
          if (lay.currentLayout) {
            currentLayoutIndex = idx;
          }
          idx++;
        }
        dataProvider = layoutNames;
        selectedIndex = currentLayoutIndex;
        invalidateDisplayList();        
      }
						
			private function onLayoutChanged(e:SwitchedLayoutEvent):void {	
        trace(LOG + " handling SwitchedLayoutEvent layout=[" + e.layoutID + "]");
        populateComboBox();
        trace(LOG + " selected layout coming in is: " + selectedIndex);
				var idx:int = -1;					
				for each (var obj:Object in dataProvider) {
					if (obj.localeKey == e.layoutID)
						idx = obj.index;
				}
				selectedIndex = idx;
				if (idx == -1) {
					prompt = e.layoutID;
				} else {
					prompt = ResourceUtil.getInstance().getString('bbb.layout.combo.prompt');
				}
				invalidateDisplayList();
				
				trace(LOG + " selected layout afterwards is: " + selectedIndex);
			}
			
			private function localeChanged(e:LocaleChangeEvent):void {
				trace(LOG + " locale change recieved");
				populateComboBox();
			}
			
			private function onSelectedItemChanged(e:Event):void {
				_dispatcher.dispatchEvent(new ChangeLayoutEvent(e.currentTarget.selectedItem.localeKey));
			}
					
		]]>
	</mx:Script>
</mx:ComboBox>
