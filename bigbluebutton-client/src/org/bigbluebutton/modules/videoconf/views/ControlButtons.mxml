<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" 
         xmlns:mate="http://mate.asfusion.com/"
         creationComplete="onCreationComplete()">
    
  <mx:Script>
    <![CDATA[
      import com.asfusion.mate.events.Dispatcher;
      
      import org.bigbluebutton.common.Images;
      import org.bigbluebutton.common.LogUtil;
      import org.bigbluebutton.core.EventConstants;
      import org.bigbluebutton.core.UsersUtil;
      import org.bigbluebutton.core.events.CoreEvent;
      import org.bigbluebutton.main.model.users.BBBUser;
      import org.bigbluebutton.main.model.users.events.KickUserEvent;
      import org.bigbluebutton.main.model.users.events.RoleChangeEvent;
      import org.bigbluebutton.modules.chat.model.ChatOptions;
      import org.bigbluebutton.modules.listeners.events.ListenersCommand;
      import org.bigbluebutton.modules.videoconf.model.VideoConfOptions;
      import org.bigbluebutton.util.i18n.ResourceUtil;
      
      public var sharerUserID:String;
      [Bindable]
      private var BUTTONS_SIZE:int = 20;
      private var BUTTONS_PADDING:int = 10;
      
      private var images:Images = new Images();
      
      [Bindable]
      private var img_mute_icon:Class = images.sound_mute;
      [Bindable]
      private var signOutIcon:Class = images.signOutIcon;
      [Bindable]
      private var adminIcon:Class = images.admin;
      [Bindable]
      private var chatIcon:Class = images.chatIcon;
      
      [Bindable]
      private var showButton:Boolean;
      
      private function onCreationComplete():void {
        showControlButtons(UsersUtil.amIPresenter());
        showPrivateChatButton();
      }
      
      public function get padding():int {
        return BUTTONS_PADDING;
      }
      
      public function handleNewRoleEvent(presenter:Boolean):void {
        showControlButtons(presenter);
      }
      
      private function onKickUserClicked():void {
        var gd:Dispatcher = new Dispatcher();
        gd.dispatchEvent(new KickUserEvent(sharerUserID)); 
      }
      
      private function onPrivateChatClicked():void {
        var e:CoreEvent = new CoreEvent(EventConstants.START_PRIVATE_CHAT);
        e.message.chatWith = sharerUserID;
        var gd:Dispatcher = new Dispatcher();
        gd.dispatchEvent(e);
      }
      
      private function onSwitchPresenterClicked():void {
        var e:RoleChangeEvent = new RoleChangeEvent(RoleChangeEvent.ASSIGN_PRESENTER);
        e.userid = sharerUserID;
        e.username = UsersUtil.getUserName(sharerUserID);
        var gd:Dispatcher = new Dispatcher();
        gd.dispatchEvent(e);     
      }
      
      private function onMuteUnmuteClicked():void {
        var bu:BBBUser = UsersUtil.getUser(sharerUserID);
        if (bu != null) {
          var e:ListenersCommand = new ListenersCommand(ListenersCommand.MUTE_USER);        
          e.userid = bu.voiceUserid;
          e.mute = ! bu.voiceMuted; 
          var gd:Dispatcher = new Dispatcher();
          gd.dispatchEvent(e);          
        }
      }
      
      private function showControlButtons(presenter:Boolean):void {
          var vidOptions:VideoConfOptions = new VideoConfOptions();
          if (UsersUtil.amIModerator()) {
            muteUnmuteBtn.visible = switchPresenter.visible = ejectUserBtn.visible = true;
          } else if (vidOptions.controlsForPresenter) {
            muteUnmuteBtn.visible = switchPresenter.visible = ejectUserBtn.visible = presenter;
          } else {
            muteUnmuteBtn.visible = switchPresenter.visible = ejectUserBtn.visible = false;
          }
          muteUnmuteBtn.toolTip = ResourceUtil.getInstance().getString('bbb.video.controls.muteButton.toolTip', [UsersUtil.getUserName(sharerUserID)]);
          switchPresenter.toolTip = ResourceUtil.getInstance().getString('bbb.video.controls.switchPresenter.toolTip', [UsersUtil.getUserName(sharerUserID)]);
          ejectUserBtn.toolTip = ResourceUtil.getInstance().getString('bbb.video.controls.ejectUserBtn.toolTip', [UsersUtil.getUserName(sharerUserID)]);
      }
      
      private function showPrivateChatButton():void {
        var chatOptions:ChatOptions = new ChatOptions();
        if (chatOptions.privateEnabled && ! UsersUtil.isMe(sharerUserID)) {
          privateChatBtn.toolTip = ResourceUtil.getInstance().getString('bbb.video.controls.privateChatBtn.toolTip', [UsersUtil.getUserName(sharerUserID)]);
          privateChatBtn.visible = true;
        } else {
          privateChatBtn.visible = false;
        }
      }
    ]]>
  </mx:Script>
  <mx:Button id="muteUnmuteBtn" visible="false" click="onMuteUnmuteClicked()" icon="{img_mute_icon}" width="{BUTTONS_SIZE}" height="{BUTTONS_SIZE}"/>
  <mx:Button id="switchPresenter" visible="false" click="onSwitchPresenterClicked()" icon="{adminIcon}" width="{BUTTONS_SIZE}" height="{BUTTONS_SIZE}"/>
  <mx:Button id="ejectUserBtn" visible="false" click="onKickUserClicked()" icon="{signOutIcon}" width="{BUTTONS_SIZE}" height="{BUTTONS_SIZE}"/>
  <mx:Button id="privateChatBtn" click="onPrivateChatClicked()" icon="{chatIcon}" width="{BUTTONS_SIZE}" height="{BUTTONS_SIZE}"/>
</mx:HBox>
