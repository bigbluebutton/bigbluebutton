<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
-->

<MDIWindow xmlns:mx="http://www.adobe.com/2006/mxml" 
			xmlns="flexlib.mdi.containers.*"
			xmlns:flc="flexlib.controls.*"
			implements="org.bigbluebutton.common.IBbbModuleWindow"
			title="{windowTitle}"
			creationComplete="onCreationComplete()" xmlns:mate="http://mate.asfusion.com/" xmlns:controls="flexlib.controls.*">
	
	<mate:Listener type="{ListenersEvent.ROOM_MUTE_STATE}" method="roomMuteStateChange" />
	<mate:Listener type="{ListenersEvent.REGISTER_LISTENERS}" method="registerListeners" />
	<mate:Listener type="{ListenersEvent.SET_LOCAL_MODERATOR_STATUS}" method="{setModerator}" />
	<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />
	<mate:Listener type="{ShortcutEvent.MUTE_ME_EVENT}" method="muteMeHandler" />
	
	<mx:Script>
		<![CDATA[
			import flash.events.Event;
			import flash.events.MouseEvent;
			import flash.sampler.getInvocationCount;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.controls.Image;
			import mx.controls.Label;
			import mx.events.ListEvent;
			import mx.messaging.channels.AMFChannel;
			
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.core.managers.UserManager;
			import org.bigbluebutton.main.events.ShortcutEvent;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.listeners.events.ListenersCommand;
			import org.bigbluebutton.modules.listeners.events.ListenersEvent;
			import org.bigbluebutton.modules.listeners.model.ListenerOptions;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			import org.bigbluebutton.common.LogUtil;
			
			private var images:Images= new Images();			
			private var _xPosition:int;
			private var _yPosition:int;
 			
			[Bindable] private var muteAllIcon:Class = images.sound_mute; 
			[Bindable] private var unmuteAllIcon:Class = images.sound_none;
			[Bindable] private var kickIcon:Class = images.eject_user;
			[Bindable] public var listeners:ArrayCollection = new ArrayCollection();
			[Bindable] public var moderator:Boolean;
			[Bindable] private var windowTitle:String = ResourceUtil.getInstance().getString('bbb.listeners.title', ["",""]);			
			[Bindable] public var listenerOptions:ListenerOptions;
			[Bindable] private var showPushToTalkBtn:Boolean = UserManager.getInstance().getConference().voiceLocked &&
				UserManager.getInstance().getConference().voiceJoined;
			
			private function onCreationComplete():void {
				BindingUtils.bindSetter(updateNumberOfListeners, listeners, "length");		
				BindingUtils.bindSetter(updateShowPushToTalkBtn, UserManager.getInstance().getConference(), "voiceLocked");
				BindingUtils.bindSetter(updateShowPushToTalkBtn, UserManager.getInstance().getConference(), "voiceJoined");
				this.visible = listenerOptions.windowVisible;
				
				if (listenerOptions.pushToTalk) {
					pushToTalkBtn.addEventListener(MouseEvent.MOUSE_DOWN, unmuteMe);
					// start up as muted when in push to talk mode
					BindingUtils.bindSetter(startVoiceAsMuted, UserManager.getInstance().getConference(), "voiceJoined");
				} else {
					pushToTalkBtn.addEventListener(MouseEvent.CLICK, toggleMuteMeState);
				}
			}
			
			private function updateShowPushToTalkBtn(show:Boolean):void {
				showPushToTalkBtn = UserManager.getInstance().getConference().voiceJoined &&
									(UserManager.getInstance().getConference().amIModerator() || 
										!UserManager.getInstance().getConference().voiceLocked);
			}
			
			private function startVoiceAsMuted(active:Boolean):void {
				if (active) {
					muteMe();
				}
			}
			
			private function updateNumberOfListeners(numListeners:int):void {
				if (numListeners > 4)
					windowTitle = ResourceUtil.getInstance().getString('bbb.listeners.title', [":", numListeners]) ;
				else 
					windowTitle = ResourceUtil.getInstance().getString('bbb.listeners.title', ["", ""]) ;
				
				if (moderator && numListeners > 0) {
					muteAllBtn.visible = true;
				} else {
					muteAllBtn.visible = false;
				}
			}
			
			//The following code block is to deal with a bug in FLexLib with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			override public function maximize():void{
				if (!isMaximized){
					savedWindowHeight = this.height;
					savedWindowWidth = this.width;
					savedX = this.x;
					savedY = this.y;
                	dispatchEvent(new MDIWindowEvent(MDIWindowEvent.MAXIMIZE, this));
                	isMaximized = true;
				} else{
					this.width = savedWindowWidth;
					this.height = savedWindowHeight;
					this.x = savedX;
					this.y = savedY;
					isMaximized = false;
					//dispatchEvent(new MDIWindowEvent(MDIWindowEvent.RESTORE, this));
				}	
			}
			
			public function getPrefferedPosition():String{
				return listenerOptions.position;
			}
			
			public function setMuteState(mute:Boolean):void{
				if (mute){
					muteAllBtn.toolTip = ResourceUtil.getInstance().getString('bbb.listeners.unmuteAllBtn.toolTip');
					muteAllBtn.selected = true;
					muteAllIcon = images.sound_mute;
				} else{
					muteAllBtn.toolTip = ResourceUtil.getInstance().getString('bbb.listeners.muteAllBtn.toolTip');
					muteAllBtn.selected = false;
					muteAllIcon = images.sound_none;
				}
			}
			
			private function toggleMuteState():void{
				if (muteAllBtn.selected) muteAllListeners();
				else if (!muteAllBtn.selected) unmuteAllListeners();
				setMuteState(muteAllBtn.selected);
			}
			
			private function toggleMuteMeState(event:MouseEvent = null):void{
				if ((UserManager.getInstance().getConference().voiceLocked && ! UserManager.getInstance().getConference().amIModerator()) ||
					listenerOptions.pushToTalk) return;
				
				var e:ListenersCommand = new ListenersCommand(ListenersCommand.MUTE_USER);
				e.userid = UserManager.getInstance().getConference().getMyVoiceUserId();
				e.mute = !UserManager.getInstance().getConference().isMyVoiceMuted();
				dispatchEvent(e);
			}
			
			private function muteMe(event:Event = null):void {
				if ((UserManager.getInstance().getConference().voiceLocked && ! UserManager.getInstance().getConference().amIModerator()) ||
					UserManager.getInstance().getConference().isMyVoiceMuted()) return;
				
				stage.removeEventListener(MouseEvent.MOUSE_UP, muteMe);
				stage.removeEventListener(KeyboardEvent.KEY_UP, muteMe);
				
				var e:ListenersCommand = new ListenersCommand(ListenersCommand.MUTE_USER);
				e.userid = UserManager.getInstance().getConference().getMyVoiceUserId();
				e.mute = true;
				dispatchEvent(e);
			}
			
			private function unmuteMe(event:MouseEvent = null):void {
				if (UserManager.getInstance().getConference().voiceLocked && ! UserManager.getInstance().getConference().amIModerator()) return;
				
				if (event != null)
					stage.addEventListener(MouseEvent.MOUSE_UP, muteMe);
				else
					stage.addEventListener(KeyboardEvent.KEY_UP, muteMe);
				
				if (UserManager.getInstance().getConference().isMyVoiceMuted()) {
					var e:ListenersCommand = new ListenersCommand(ListenersCommand.MUTE_USER);
					e.userid = UserManager.getInstance().getConference().getMyVoiceUserId();
					e.mute = false;
					dispatchEvent(e);
				}
			}
			
			private function muteAllListeners():void{
				var muteCommand:ListenersCommand = new ListenersCommand(ListenersCommand.MUTE_ALL);
				dispatchEvent(muteCommand);
			}
			
			private function unmuteAllListeners():void{
				var unmuteCommand:ListenersCommand = new ListenersCommand(ListenersCommand.UNMUTE_ALL);
				dispatchEvent(unmuteCommand);
			}
			
			private function roomMuteStateChange(e:ListenersEvent):void{
				setMuteState(e.mute_state);
			}
			
			private function registerListeners(e:ListenersEvent):void{
				this.listeners = e.listeners.listeners;
				/*
				 * Bind into this object to display the number of listeners.
				 */
				BindingUtils.bindSetter(updateNumberOfListeners, listeners, "length");
			}
			
			private function setModerator(e:ListenersEvent):void{
				moderator = e.moderator;
				showCloseButton = false;
			}
						
			private function onItemRollOver(e:ListEvent):void{
				var item:ListenerItem = e.itemRenderer as ListenerItem;
				item.onRollOver();
			}
			
			private function onItemRollOut(e:ListEvent):void{
				var item:ListenerItem = e.itemRenderer as ListenerItem;
				item.onRollOut();
			}
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();				
				if (listeners.length > 4)
					windowTitle = ResourceUtil.getInstance().getString('bbb.listeners.title', [":", listeners.length]);
				else 
					windowTitle = ResourceUtil.getInstance().getString('bbb.listeners.title', ["",""]);
			}
			
			private function localeChanged(e:LocaleChangeEvent):void{
				resourcesChanged();
			}
			
			private function muteMeHandler(e:ShortcutEvent):void{
				if (listenerOptions.pushToTalk)
					unmuteMe();
				else
					toggleMuteMeState();
			}
		]]>
	</mx:Script>
    	<mx:List id="listenersList" height="100%" width="100%"
 			itemRenderer="org.bigbluebutton.modules.listeners.views.ListenerItem" 
            dragEnabled="false" dataProvider="{listeners}"
			itemRollOver="onItemRollOver(event)"
			itemRollOut="onItemRollOut(event)">          
    	</mx:List>				
    	    	
    	<mx:ControlBar width="100%">
			<mx:Button id="pushToTalkBtn" visible="{showPushToTalkBtn}" height="30" maxWidth="180" horizontalGap="0" paddingLeft="5" paddingTop="5" paddingRight="5" paddingBottom="5"
					   toolTip="{UserManager.getInstance().getConference().voiceMuted ? ResourceUtil.getInstance().getString('bbb.listeners.pushToTalk.toolTip') : ResourceUtil.getInstance().getString('bbb.listeners.pushToMute.toolTip')}"
					   label="{UserManager.getInstance().getConference().voiceMuted ? ResourceUtil.getInstance().getString('bbb.listeners.talk') : ResourceUtil.getInstance().getString('bbb.listeners.mute')}" 
					   icon="{UserManager.getInstance().getConference().voiceMuted ? images.sound_mute : images.sound_none}" />
     	   	<mx:Spacer width="100%"/>
			<mx:Button id="muteAllBtn" visible="false" toggle="true" selected="false" click="toggleMuteState()" height="30" maxWidth="180"
					   horizontalGap="0" paddingLeft="5" paddingTop="5" paddingRight="5" paddingBottom="5"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.listeners.muteAllBtn.toolTip')}" 
					   label="{muteAllBtn.selected ?  ResourceUtil.getInstance().getString('bbb.listeners.unmuteAllBtn.toolTip') : ResourceUtil.getInstance().getString('bbb.listeners.muteAllBtn.toolTip')}" />
    	</mx:ControlBar>

</MDIWindow>
