<?xml version="1.0" encoding="utf-8"?>

<!--
  BigBlueButton open source conferencing system - http://www.bigbluebutton.org
  
  Copyright (c) 2010 BigBlueButton Inc. and by respective authors (see below).
  
  BigBlueButton is free software; you can redistribute it and/or modify it under the 
  terms of the GNU Lesser General Public License as published by the Free Software 
  Foundation; either version 2.1 of the License, or (at your option) any later 
  version. 
  
  BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY 
  WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
  PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
  
  You should have received a copy of the GNU Lesser General Public License along 
  with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.
 
  $Id: $
-->

<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
		xmlns:flexmdi="flexlib.mdi.containers.*"
		xmlns:logModule="org.bigbluebutton.modules.log.view.components.*"
		xmlns:views="org.bigbluebutton.main.views.*"
		xmlns:mate="http://mate.asfusion.com/"
		xmlns:maps="org.bigbluebutton.main.maps.*"
		xmlns:api="org.bigbluebutton.main.api.*"
		width="100%" height="100%"
		creationComplete="initializeShell()" initialize="allowCallBack()">
		
	<mate:Listener type="{ConfigEvent.CONFIG_EVENT}" method="setLogVisibility" />
	<mate:Listener type="{OpenWindowEvent.OPEN_WINDOW_EVENT}" method="handleOpenWindowEvent" />	
	<mate:Listener type="{CloseWindowEvent.CLOSE_WINDOW_EVENT}" method="handleCloseWindowEvent"/>
	<mate:Listener type="{AddUIComponentToMainCanvas.ADD_COMPONENT}" method="addComponentToCanvas" />
	<mate:Listener type="{AppVersionEvent.APP_VERSION_EVENT}" method="handleApplicationVersionEvent" />	
	<mate:Listener type="{ConnectionFailedEvent.USER_LOGGED_OUT}" method="handleLogout" />
	<mate:Listener type="{ConnectionFailedEvent.CONNECTION_FAILED}" method="attemptReconnect" />
	<mate:Listener type="{ConnectionFailedEvent.CONNECTION_CLOSED}" method="attemptReconnect"  />
	<mate:Listener type="{ConnectionFailedEvent.UNKNOWN_REASON}" method="attemptReconnect"  />
	<mate:Listener type="{ConnectionFailedEvent.CONNECTION_REJECTED}" method="attemptReconnect"  />

	<api:APIEventMap />
	
	<mx:Script>
		<![CDATA[
			import org.bigbluebutton.main.events.AppVersionEvent;
			import com.asfusion.mate.events.Dispatcher;
			
			import flash.events.MouseEvent;
			
			import flexlib.mdi.containers.MDIWindow;
			import flexlib.mdi.effects.effectsLib.MDIVistaEffects;
			
			import mx.collections.ArrayCollection;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			
			import org.bigbluebutton.common.IBbbModuleWindow;
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.main.events.AddUIComponentToMainCanvas;
			import org.bigbluebutton.main.events.CloseWindowEvent;
			import org.bigbluebutton.main.events.ConfigEvent;
			import org.bigbluebutton.main.events.ModuleLoadEvent;
			import org.bigbluebutton.main.events.OpenWindowEvent;
			import org.bigbluebutton.main.events.PortTestEvent;
			import org.bigbluebutton.main.events.SuccessfulLoginEvent;
			import org.bigbluebutton.main.model.users.events.ConnectionFailedEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			import org.bigbluebutton.util.logging.Logger;
			
			// _PTS_731_
            import flash.external.*;
            import flash.utils.Timer;
            import flash.events.TimerEvent;
            import mx.events.CloseEvent;
            import mx.managers.PopUpManager;
            import mx.containers.TitleWindow;
           
            import mx.containers.HBox;
            import mx.controls.Image;
            
            // _PST_731_
            private var _gWindow:TitleWindow;
            private var gTimRaiseHand:Timer;
            private var gTimChat:Timer;
			private var gTimNewUser:Timer;
            private const TIMER_INTERVAL:int    = 1000;
            private const NOTIFY_DURATION:int   = 20;
            private var gNotifyRaiseNum:Number   = 0;
            private var gIsPresentationFullScr:Boolean      = false;
            private var gIsEnableChatNotification:Boolean   = true;
            private var gHBoxNotification:HBox;
			
			private var globalDispatcher:Dispatcher;
			
			private var dispState:String; //full-screen?
			private var images:Images = new Images();
			private var stoppedModules:ArrayCollection;
			
			private var logs:Logger = new Logger();
			private var logWindow:LogWindow;
			
			private var logoutWindow:LoggedOutWindow;
			private var connectionLostWindow:ConnectionLostWindow;
			
			// LIVE or PLAYBACK
			private var _mode:String = 'LIVE';
			[Bindable] public var appVersion:String = '0.7';
			private var localeVersion:String = 'old';
			[Bindable] public var numberOfModules:int = 0;
			
			[Bindable] private var fullscreen_icon:Class = images.full_screen;
			[Bindable] private var logs_icon:Class = images.table;
			[Bindable] private var reset_layout_icon:Class = images.layout;
			
            // _PTS_731_
			[Bindable] private var handIcon:Class    = images.raisehand;
			[Bindable] private var disable_chat_notify:Class = images.disable_chat_notify;
			[Bindable] private var enable_chat_notify:Class  = images.enable_chat_notify;
			
			// icon for new user notification box
			[Bindable] private var userIcon:Class    = images.user;
			
			public function get mode():String {
				return _mode;
			}
			
			protected function initializeShell():void {		
				trace('Initializing');	
				globalDispatcher = new Dispatcher();
			}

			protected function initFullScreen():void {				
				/* Set up full screen handler. */
				stage.addEventListener(FullScreenEvent.FULL_SCREEN, fullScreenHandler);
				dispState = stage.displayState;
			}					
			
			private function handleApplicationVersionEvent(event:AppVersionEvent):void {
				appVersion = event.appVersion;
				localeVersion = event.localVersion;
				checkLocaleVersion(localeVersion);
			}
			
			private function fullScreenHandler(evt:FullScreenEvent):void {
				dispState = stage.displayState + " (fullScreen=" + evt.fullScreen.toString() + ")";
				if (evt.fullScreen) {
					LogUtil.debug("Switching to full screen");
					/* Do something specific here if we switched to full screen mode. */
				
				} else {
					LogUtil.debug("Switching to normal screen");
					/* Do something specific here if we switched to normal mode. */
				}
			}			
			
			private function openLogWindow():void {
				if (logWindow == null){
					logWindow = new LogWindow();
					logWindow.logs = logs;
				}
				mdiCanvas.windowManager.add(logWindow);
				mdiCanvas.windowManager.absPos(logWindow, 50, 50);
				logWindow.width = mdiCanvas.width - 100;
				logWindow.height = mdiCanvas.height - 100;
			}
			
			private function toggleFullScreen():void{
	   			LogUtil.debug("Toggling fullscreen");
	   			try {
					switch (stage.displayState) {
						case StageDisplayState.FULL_SCREEN:
							LogUtil.debug("full screen mode");
							// If already in full screen mode, switch to normal mode.
							stage.displayState = StageDisplayState.NORMAL;
							break;
						default:
							LogUtil.debug("Normal screen mode");
							// If not in full screen mode, switch to full screen mode.
							stage.displayState = StageDisplayState.FULL_SCREEN;
							break;
					}
				} catch (err:SecurityError) {
					// ignore
				}
	   		}	
	   		
	   		private function handleOpenWindowEvent(event:OpenWindowEvent):void {
	   			
	   			var window:IBbbModuleWindow = event.window;
	   			mdiCanvas.addWindow(window);
	   		}
	   		
	   		private function handleCloseWindowEvent(event:CloseWindowEvent):void {
	   			var window:IBbbModuleWindow = event.window;
				mdiCanvas.removeWindow(window);
	   		}
	   		
	   		private function resetLayout():void{
	   			mdiCanvas.resetWindowLayout();
	   		}
	   		
	   		private function addComponentToCanvas(e:AddUIComponentToMainCanvas):void{
	   			mdiCanvas.addChild(e.component);
	   		}
	   		
	   		public function checkLocaleVersion(localeVersion:String):void {	   			
	   			Alert.okLabel ="OK";
				var version:String = "old-locales";
				version = ResourceUtil.getInstance().getString('bbb.mainshell.locale.version');
				LogUtil.debug("Locale from config=" + localeVersion + ", from locale file=" + version);

				if ((version == "old-locales") || (version == "") || (version == null)) {
					wrongLocaleVersion();
				} else {
					if (version != localeVersion) wrongLocaleVersion();
				}	   			
	   		}
	   		
	   		private function wrongLocaleVersion():void {
				var localeWindow:OldLocaleWarnWindow = OldLocaleWarnWindow(PopUpManager.createPopUp(mdiCanvas, OldLocaleWarnWindow, false));

				var point1:Point = new Point();
	        	// Calculate position of TitleWindow in Application's coordinates. 
	        	point1.x = 400;
	        	point1.y = 300;                
	        	point1 = mdiCanvas.localToGlobal(point1);
				localeWindow.x = point1.x + 25;
				localeWindow.y = point1.y + 25;	
            }
			
			private function handleLogout(e:ConnectionFailedEvent):void{
				if (logoutWindow != null) return;
				logoutWindow = LoggedOutWindow(PopUpManager.createPopUp( mdiCanvas, LoggedOutWindow, false));
				
				var point1:Point = new Point();
				// Calculate position of TitleWindow in Application's coordinates. 
				point1.x = 400;
				point1.y = 300;                
				point1 = localToGlobal(point1);
				logoutWindow.x = point1.x + 25;
				logoutWindow.y = point1.y + 25;	
				if (e is ConnectionFailedEvent) logoutWindow.setReason((e as ConnectionFailedEvent).type);
				else logoutWindow.setReason("You have logged out of the conference");
				
				mdiCanvas.removeAllWindows(); 
			}
			
			private function attemptReconnect(e:ConnectionFailedEvent):void{
				if (connectionLostWindow != null) return;
				connectionLostWindow = ConnectionLostWindow(PopUpManager.createPopUp( mdiCanvas, ConnectionLostWindow, true));
				connectionLostWindow.x = mdiCanvas.width/2 - connectionLostWindow.width/2;
				connectionLostWindow.y = mdiCanvas.height/2 - connectionLostWindow.height/2;
			}
			
			private function setLogVisibility(e:ConfigEvent):void{
				logBtn.visible = e.config.showDebug;
			}
			
            /*****************************************************************************
            ;  changeChatNotificationStatus
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to switch enable or disable of chat notification
            ;   while presentation in fullscreen mode.
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ; 
            ;       INPUT : N/A
            ; 
            ; IMPLEMENTATION
            ;  
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            private function changeChatNotificationStatus():void{
            
                // determine that chat notification is enbaled.
                if( true == gIsEnableChatNotification)
                {
                    // change chat notification button icon and tooltip
                    btnEnbChatAlert.toolTip     = ResourceUtil.getInstance().getString('bbb.mainshell.enableChatNotification');
                    gIsEnableChatNotification   = false;
                    btnEnbChatAlert.setStyle("icon",enable_chat_notify); 
                }
                else{
                    // change chat notification button icon and tooltip
                    btnEnbChatAlert.toolTip     = ResourceUtil.getInstance().getString('bbb.mainshell.disableChatNotification');
                    gIsEnableChatNotification   = true;
                    btnEnbChatAlert.setStyle("icon",disable_chat_notify); 
                }
            }
            /*
             * END: changeChatNotificationStatus
             */

            /*****************************************************************************
            ; allowCallBack
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to add function that will be call from javascript.
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ; 
            ;       INPUT : N/A
            ;
            ; 
            ; IMPLEMENTATION
            ;  
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            public function allowCallBack():void
            {
                // determine that ExternalInterface object supported.
                if (true == ExternalInterface.available)
                {
                    // add function that can call back from javascript
                    ExternalInterface.addCallback("showButtonCallBack", showHideButton);
                    ExternalInterface.addCallback("raiseHandCallback", participantRaisedHand);
                    ExternalInterface.addCallback("newChatCallback", handleNewChat);
                    ExternalInterface.addCallback("handleNewUserCallback", handleNewUser);
                }
            }
            /***
            ; END: allowCallBack
            ****/
             
            /*****************************************************************************
            ;  showHideButton
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to display enable or disable chat notification button.
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ; 
            ;       INPUT
            ;           lIsVisible: status of presentation window "if in full screen mode
            ;                       the status is isVisible".
            ; 
            ; IMPLEMENTATION
            ;  
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            public function showHideButton(lIsVisible:Boolean):void
            {
                gIsPresentationFullScr       = lIsVisible;
                btnEnbChatAlert.visible      = lIsVisible;
            }
            /*
             * END: showHideButton
             */
            
            /*****************************************************************************
            ;  participantRaisedHand
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to call when participant raise hand to notify 
            ;   raise hand event while presentation window in full screen mode.
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       lName: the name of participant that raised hand.
            ; 
            ; IMPLEMENTATION
            ;   use Timer to display blink message and hand icon
            ;
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            public function participantRaisedHand(lName:String):void
            {
                // determine the presentation window not in
                // full screen mode.
                if ( false == gIsPresentationFullScr)
                {
                    return;
                }
                // determine timer object to stop it
                if (null != gTimRaiseHand)
                {
                    gTimRaiseHand.stop();
                    gNotifyRaiseNum = 0;
                }
                lblNotification.text         = lName;
                lblNotification.visible      = false;
                btnEnbRaiseHandAlert.visible = false;
                
                // initialize and start timer.
                gTimRaiseHand = new Timer(TIMER_INTERVAL);
                gTimRaiseHand.addEventListener(TimerEvent.TIMER, updateTimer);
                gTimRaiseHand.start();
            }
            /*
             *  END: participantRaisedHand
             */
             
            /*****************************************************************************
            ;  updateTimer
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to call by Timer display blink message 
            ;   with hand icon. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       evt :   TimerEvent object.
            ; 
            ; IMPLEMENTATION
            ;
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            private function updateTimer(evt:TimerEvent):void
            {
                // determine the timer step 'equal to NOTIFY_DURATION'
                if (NOTIFY_DURATION == gNotifyRaiseNum)
                {
                    // hide raiseHand icon and label
                    lblNotification.visible = false;
                    btnEnbRaiseHandAlert.visible = false;
                    
                    // stop the timer.
                    if ( null != gTimRaiseHand ){
                        gTimRaiseHand.stop();
                    }
                    gNotifyRaiseNum = 0;
                    return;
                } 
                lblNotification.visible      = !lblNotification.visible;
                btnEnbRaiseHandAlert.visible = !btnEnbRaiseHandAlert.visible;
                gNotifyRaiseNum += 1;
            }
            /*
             *  END: updateTimer
             */
             
            /*****************************************************************************
            ;  handleNewChat
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to call when participant raise hand to notify 
            ;   new chat event while presentation window in full screen mode. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       lSender  :   the name of sender.
            ;       lMessage :   chat message.
            ; 
            ; IMPLEMENTATION
            ;
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            private function handleNewChat(lSender:String,lMessage:String):void
            {
                // check presentation window if not full screen
                // or chat notification not enable, function return
                if ( false == gIsPresentationFullScr
                        ||
                     false == gIsEnableChatNotification
                     )
                {
                    return;
                }
                if (null != gTimChat)
                {
                    // check 'gTimChat' Timer and stop if it's starting.
                    gTimChat.stop();
                    PopUpManager.removePopUp(_gWindow);
                }
                if ( null != gTimNewUser )
                {
                    // new user notification running, stop it.
                    gTimNewUser.stop();
                    PopUpManager.removePopUp(_gWindow);
                }
                
                // initialize chat notification window
                showWindow(lSender,lMessage);
                
                // initialize Timer and start to blink chat notification
                gTimChat = new Timer(TIMER_INTERVAL, NOTIFY_DURATION);
				gTimChat.addEventListener(TimerEvent.TIMER, function(evt:TimerEvent):void{
					gHBoxNotification.visible    = !gHBoxNotification.visible;
					}
				);
                gTimChat.start();
                
            }
            /*
             *  END: handleNewChat
             */

            /*****************************************************************************
            ;  showWindow
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to initialize chat notification window. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       lName   :    the name of participant.
            ;       lMessage:    new chat message.
            ; 
            ; IMPLEMENTATION
            ;   use PopupManager to create popup chat notification window
            ;
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            private function showWindow(lName:String,lMessage:String):void{
                
                var lblChatNotificaton:Label    = new Label;
                var lNotificationMsg:String      = lName + " : " + lMessage;
                gHBoxNotification    = new HBox;
                   
                // determine the message length is > 25 character
                if ( 25 < lNotificationMsg.length )
                {
                    // keep message to display only 25 character.
                    lNotificationMsg     = lNotificationMsg.substring(0,25) + "....";
                }
                lblChatNotificaton.text = lNotificationMsg;
                gHBoxNotification.addChild(lblChatNotificaton);
                
                // initialize and add message to notification window.
                _gWindow         = TitleWindow(PopUpManager.createPopUp(this, TitleWindow));
                _gWindow.title  = 'New Chat';
                _gWindow.addChild(gHBoxNotification); 
                _gWindow.width  = 200;
                _gWindow.horizontalScrollPolicy="off";
                
                // set position
                _gWindow.x   = mdiCanvas.width  - _gWindow.width;
                _gWindow.y   = mdiCanvas.height - _gWindow.height + 15; 
                _gWindow.showCloseButton = true;
                _gWindow.addEventListener(CloseEvent.CLOSE, function(evt:CloseEvent):void{
					PopUpManager.removePopUp(_gWindow);
					}
				);
            }
            /*
             *  END: showWindow
             */

            /*****************************************************************************
            ;  createNewUserNotificationWindow
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to initialize new participant joined 
            ;   notification window. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       lUname: the name of new participant.
            ;
            ; IMPLEMENTATION
            ;   use PopupManager to create popup notification window
            ;
            ; HISTORY
            ; __date__ :        PTS:    Description
            ; 2010.12.16                new participant joined notification
            ;
            ******************************************************************************/
            private function createNewUserNotificationWindow(lUname:String) : void{
                
                // initialize controls to add to notification window
                var imgUserIcon:Image   = new Image;
                var lblNotificationText:Label = new Label;
                gHBoxNotification    = new HBox;
                
                imgUserIcon.source      = userIcon;
                lblNotificationText.text= lUname + " : joined";
                lblNotificationText.width   = 150;
                
                gHBoxNotification.addChild(imgUserIcon);
                gHBoxNotification.addChild(lblNotificationText);
                
                // initialize and add controls to notification window.
                _gWindow = TitleWindow(PopUpManager.createPopUp(this, TitleWindow));
                _gWindow.title = 'Notification';
                _gWindow.addChild(gHBoxNotification);
                _gWindow.width   = 200;
                _gWindow.horizontalScrollPolicy="off";
                    
                // set position
                _gWindow.x   = mdiCanvas.width  - _gWindow.width;
                _gWindow.y   = mdiCanvas.height - _gWindow.height + 15; 
                _gWindow.showCloseButton = true;
                _gWindow.addEventListener(CloseEvent.CLOSE, function(evt:CloseEvent):void{
					PopUpManager.removePopUp(_gWindow);
					}
				); 
            }
            /*
             *  END: createNewUserNotificationWindow
             */

             
            /*****************************************************************************
            ;  closeHandler
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to handle close button event of chat
            ;   notification window. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES
            ;   INPUT
            ;       event: CloseEvent object
            ; IMPLEMENTATION
            ;   
            ; HISTORY
            ; __date__ :        PTS:  
            ; 2010.12.06        problem 731
            ;
            ******************************************************************************/
            private function closeHandler(event:CloseEvent):void{
                // remove notification window.
                PopUpManager.removePopUp(_gWindow);
            }
            /*
             *  END: closeHandler
             */
             
            /*****************************************************************************
            ;  handleNewUser
            ;----------------------------------------------------------------------------
            ; DESCRIPTION
            ;   This routine is use to call when new participant joined to notify 
            ;   other participant while presentation window in full screen mode. 
            ;
            ; RETURNS : N/A
            ;
            ; INTERFACE NOTES    
            ;   INPUT
            ;       lUname: the name of new participant.
            ;
            ; IMPLEMENTATION
            ;
            ; HISTORY
            ; __date__ :        PTS:    Description
            ; 2010.12.16                new participant joined notification
            ;
            ******************************************************************************/
            private function handleNewUser(lUname:String) : void{
                // determine the presentation window not in full
                // screen mode.
                if ( false == gIsPresentationFullScr ){
                    return;
                }
                if ( null != gTimChat ){
                    // stop 'gTimChat' timer.
                    gTimChat.stop();
                    PopUpManager.removePopUp(_gWindow);
                }
                if ( null != gTimNewUser ){
                    // timer already running
                    gTimNewUser.stop();
                    PopUpManager.removePopUp(_gWindow);
                }
                // create notification window
                createNewUserNotificationWindow(lUname);
                
                // initialize and start time
                gTimNewUser = new Timer(TIMER_INTERVAL, NOTIFY_DURATION);
				gTimNewUser.addEventListener(TimerEvent.TIMER, function(evt:TimerEvent):void{
					gHBoxNotification.visible = !gHBoxNotification.visible;
					}
				);
                gTimNewUser.start();
            }
            /*
             *  END: handleNewUser
             */
             
		]]>
	</mx:Script>
			
	<views:MainToolbar id="toolbar" dock="true" width="100%" height="30" visible="false" verticalAlign="middle"/>
	<views:MainCanvas id="mdiCanvas" horizontalScrollPolicy="off" verticalScrollPolicy="off" effectsLib="{flexlib.mdi.effects.effectsLib.MDIVistaEffects}" width="100%" height="100%">
		<views:LoadingBar id="progressBar" x="{this.width/2 - progressBar.width/2}" y="{this.height/2 - progressBar.height/2}" width="{this.width/2}" />
	</views:MainCanvas>	
	<mx:ControlBar width="100%" height="20" paddingTop="0">		
	  	<mx:Label text="{ResourceUtil.getInstance().getString('bbb.mainshell.copyrightLabel2',[appVersion])}" id="copyrightLabel2"/>
	 	<mx:Spacer width="20"/>
		<mx:Spacer width="100%"/> 
        
        <!-- _PTS_731_ -->
		<mx:Label text="" visible="false" id="lblNotification" color="#FFFFFF"/>
		<mx:Button width="20" height="20" toolTip="" visible="false" id="btnEnbRaiseHandAlert" icon="{handIcon}" />
		<mx:Button width="20" height="20" toolTip="{ResourceUtil.getInstance().getString('bbb.mainshell.disableChatNotification')}" visible="false" id="btnEnbChatAlert" icon="{disable_chat_notify}" click="changeChatNotificationStatus()" />
	    <!--===========-->
	    
		<mx:Button width="20" height="20" toolTip="{ResourceUtil.getInstance().getString('bbb.mainshell.logBtn.toolTip')}" id="logBtn" icon="{logs_icon}" click="openLogWindow()" />
		<mx:Button width="20" height="20" toolTip="{ResourceUtil.getInstance().getString('bbb.mainshell.resetLayoutBtn.toolTip')}" id="btnResetLayout" icon="{reset_layout_icon}" click="resetLayout()" />
	</mx:ControlBar>
</mx:VBox>
