<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.


BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<mx:VBox xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:flexmdi="flexlib.mdi.containers.*"
		xmlns:logModule="org.bigbluebutton.modules.log.view.components.*"
		xmlns:views="org.bigbluebutton.main.views.*"
		xmlns:mate="http://mate.asfusion.com/"
		xmlns:maps="org.bigbluebutton.main.maps.*"
		xmlns:api="org.bigbluebutton.main.api.*"
		xmlns:common="org.bigbluebutton.common.*"
		width="100%" height="{parentApplication.height - 1}"
		horizontalScrollPolicy="off" verticalScrollPolicy="off"
		styleName="mainVBox" verticalGap="0"
		creationComplete="initializeShell()" >

	<!--
	height="{parentApplication.height - 1}" : The container height is set to fix height because the percentHeight
	generates a script execution timeout in the GraphicsWrapper class. We also remove one pixel to force the first
	update as the height property needs a value different from thec current one to be updated.
		
	-->
	<fx:Declarations>
		<mate:Listener type="{OpenWindowEvent.OPEN_WINDOW_EVENT}" method="handleOpenWindowEvent" />	
		<mate:Listener type="{CloseWindowEvent.CLOSE_WINDOW_EVENT}" method="handleCloseWindowEvent"/>
		<mate:Listener type="{AddUIComponentToMainCanvas.ADD_COMPONENT}" method="addComponentToCanvas" />
		<mate:Listener type="{AppVersionEvent.APP_VERSION_EVENT}" method="handleApplicationVersionEvent" />	
		<mate:Listener type="{MeetingNotFoundEvent.MEETING_NOT_FOUND}" method="handleMeetingNotFoundEvent" />
		<mate:Listener type="{ConnectionFailedEvent.USER_LOGGED_OUT}" method="handleLogout" />
	    <mate:Listener type="{ConnectionFailedEvent.USER_EJECTED_FROM_MEETING}" method="handleLogout" />
		<mate:Listener type="{ConnectionFailedEvent.CONNECTION_FAILED}" method="attemptReconnect" />
		<mate:Listener type="{ConnectionFailedEvent.CONNECTION_CLOSED}" method="attemptReconnect"  />
		<mate:Listener type="{ConnectionFailedEvent.UNKNOWN_REASON}" method="attemptReconnect"  />
		<mate:Listener type="{ConnectionFailedEvent.CONNECTION_REJECTED}" method="attemptReconnect"  />
		<mate:Listener type="{ExitApplicationEvent.EXIT_APPLICATION}" method="handleExitApplicationEvent" />
		<mate:Listener type="{ConfigLoadedEvent.CONFIG_LOADED_EVENT}" method="initOptions"  />
		<mate:Listener type="{FlashMicSettingsEvent.FLASH_MIC_SETTINGS}" method="handleFlashMicSettingsEvent"  />
		<mate:Listener type="{WebRTCEchoTestEvent.WEBRTC_ECHO_TEST_CONNECTING}" method="handleWebRTCEchoTestConnectingEvent"  />
		<mate:Listener type="{WebRTCMediaEvent.WEBRTC_MEDIA_REQUEST}" method="handleWebRTCMediaRequestEvent"  />
		<mate:Listener type="{WebRTCCallEvent.WEBRTC_CALL_STARTED}" method="handleWebRTCCallStartedEvent" />
		<mate:Listener type="{WebRTCCallEvent.WEBRTC_CALL_ENDED}" method="handleWebRTCCallEndedEvent" />
		<mate:Listener type="{AudioSelectionWindowEvent.SHOW_AUDIO_SELECTION}" method="handleShowAudioSelectionWindowEvent" />
		<mate:Listener type="{ShareCameraRequestEvent.SHARE_CAMERA_REQUEST}" method="handleShareCameraRequestEvent" />
	  
		<mate:Listener type="{ToolbarButtonEvent.ADD}" method="handleAddToolbarComponent" />	
		<mate:Listener type="{ToolbarButtonEvent.REMOVE}" method="handleRemoveToolbarComponent"/>
		<mate:Listener type="{ShortcutEvent.OPEN_SHORTCUT_WIN}" method="openShortcutHelpWindow" />
		<mate:Listener type="{BBBEvent.OPEN_WEBCAM_PREVIEW}" method="openVideoPreviewWindow" />
		<mate:Listener type="{BBBEvent.INACTIVITY_WARNING_EVENT}" method="handleInactivityWarningEvent" />
		<mate:Listener type="{LockControlEvent.OPEN_LOCK_SETTINGS}" method="openLockSettingsWindow" />
		<mate:Listener type="{BreakoutRoomEvent.OPEN_BREAKOUT_ROOMS_PANEL}" method="openBreakoutRoomsWindow" />
		<mate:Listener type="{InvalidAuthTokenEvent.INVALID_AUTH_TOKEN}" method="handleInvalidAuthToken" />
		
		<mate:Listener type="{SwitchedLayoutEvent.SWITCHED_LAYOUT_EVENT}" method="onLayoutChanged" />
	
		<mate:Listener type="{NetworkStatsEvent.OPEN_NETSTATS_WIN}" method="openNetworkStatsWindow" />
		<mate:Listener type="{BBBEvent.RETRIEVE_GUEST_POLICY}" method="setGuestPolicy"/>
		<mate:Listener type="{ConnectionFailedEvent.MODERATOR_DENIED_ME}" method="handleLogout" />
		<mate:Listener type="{BBBEvent.MODERATOR_ALLOWED_ME_TO_JOIN}" method="guestAllowed" />
		<mate:Listener type="{RefreshGuestEvent.REFRESH_GUEST_VIEW}" method="refreshGuestView" />
		<mate:Listener type="{BBBEvent.REMOVE_GUEST_FROM_LIST}" method="removeGuestWindow" />
		<mate:Listener type="{BBBEvent.WAITING_FOR_MODERATOR_ACCEPTANCE}" method="openWaitWindow" />
		<mate:Listener type="{BBBEvent.RECONNECT_DISCONNECTED_EVENT}" method="closeWaitWindow"/>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import flash.events.Event;
			import flash.events.FullScreenEvent;
			import flash.events.IOErrorEvent;
			import flash.events.TextEvent;
			import flash.geom.Point;
			import flash.net.navigateToURL;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.core.IFlexDisplayObject;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			
			import flexlib.mdi.effects.effectsLib.MDIVistaEffects;
			
			import org.as3commons.lang.StringUtils;
			import org.as3commons.logging.api.ILogger;
			import org.as3commons.logging.api.getClassLogger;
			import org.bigbluebutton.common.IBbbModuleWindow;
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.events.AddUIComponentToMainCanvas;
			import org.bigbluebutton.common.events.CloseWindowEvent;
			import org.bigbluebutton.common.events.OpenWindowEvent;
			import org.bigbluebutton.common.events.ToolbarButtonEvent;
			import org.bigbluebutton.core.BBB;
			import org.bigbluebutton.core.Options;
			import org.bigbluebutton.core.PopUpUtil;
			import org.bigbluebutton.core.UsersUtil;
			import org.bigbluebutton.core.events.LockControlEvent;
			import org.bigbluebutton.core.events.SwitchedLayoutEvent;
			import org.bigbluebutton.core.vo.LockSettingsVO;
			import org.bigbluebutton.main.events.AppVersionEvent;
			import org.bigbluebutton.main.events.BBBEvent;
			import org.bigbluebutton.main.events.BreakoutRoomEvent;
			import org.bigbluebutton.main.events.ClientStatusEvent;
			import org.bigbluebutton.main.events.ConfigLoadedEvent;
			import org.bigbluebutton.main.events.ExitApplicationEvent;
			import org.bigbluebutton.main.events.InvalidAuthTokenEvent;
			import org.bigbluebutton.main.events.MeetingNotFoundEvent;
			import org.bigbluebutton.main.events.ModuleLoadEvent;
			import org.bigbluebutton.main.events.NetworkStatsEvent;
			import org.bigbluebutton.main.events.RefreshGuestEvent;
			import org.bigbluebutton.main.events.ShortcutEvent;
			import org.bigbluebutton.main.model.Guest;
			import org.bigbluebutton.main.model.ImageLoader;
			import org.bigbluebutton.main.model.options.BrandingOptions;
			import org.bigbluebutton.main.model.options.BrowserVersionsOptions;
			import org.bigbluebutton.main.model.options.LanguageOptions;
			import org.bigbluebutton.main.model.options.LayoutOptions;
			import org.bigbluebutton.main.model.users.events.ConnectionFailedEvent;
			import org.bigbluebutton.modules.phone.events.AudioSelectionWindowEvent;
			import org.bigbluebutton.modules.phone.events.FlashMicSettingsEvent;
			import org.bigbluebutton.modules.phone.events.WebRTCCallEvent;
			import org.bigbluebutton.modules.phone.events.WebRTCEchoTestEvent;
			import org.bigbluebutton.modules.phone.events.WebRTCMediaEvent;
			import org.bigbluebutton.modules.phone.models.PhoneOptions;
			import org.bigbluebutton.modules.users.views.BreakoutRoomSettings;
			import org.bigbluebutton.modules.videoconf.events.ShareCameraRequestEvent;
			import org.bigbluebutton.util.i18n.ResourceUtil;
			
			private static const LOGGER:ILogger = getClassLogger(MainApplicationShell);      
      
			private var globalDispatcher:Dispatcher;			
			private var dispState:String; //full-screen?
			private var images:Images = new Images();
			private var stoppedModules:ArrayCollection;			
			private var logWindow:LogWindow;
			private var waitWindow:WaitingWindow = null;
			private var guestWindow:GuestWindow = null;
			private var scWindow:ShortcutHelpWindow;
			private var connectionLostWindow:ConnectionLostWindow;
			
			// LIVE or PLAYBACK
			private var _mode:String = 'LIVE';
			[Bindable] public var appVersion:String = ' ';
			private var localeVersion:String = 'old';
			[Bindable] public var numberOfModules:int = 0;
			
			[Bindable] private var fullscreen_icon:Class = images.full_screen;
			[Bindable] private var logs_icon:Class = images.table;

			private var receivedConfigLocaleVer:Boolean = false;
			private var receivedResourceLocaleVer:Boolean = false;
			
			[Bindable] private var copyrightText:String;

			private var _hideToolbarsTimer:Timer;

			public function get mode():String {
				return _mode;
			}
			
			[Bindable] private var layoutOptions:LayoutOptions;
			[Bindable] private var brandingOptions:BrandingOptions;
			
			[Bindable] private var showToolbarOpt:Boolean = true;
			[Bindable] private var _showToolbar:Boolean = true;
			private const DEFAULT_TOOLBAR_HEIGHT:Number = 50;
			[Bindable] private var toolbarHeight:Number = DEFAULT_TOOLBAR_HEIGHT;
			[Bindable] private var toolbarPaddingTop:Number = 2;
			[Bindable] private var showFooterOpt:Boolean = true;
			[Bindable] private var _showFooter:Boolean = true;
			[Bindable] private var footerHeight:Number = 60;
			
			[Bindable] private var isTunneling:Boolean = false;
			
			private var guestManagement:GuestManagement = null;
			private var guest:Guest = new Guest();
			private var guestPolicy:String = "ASK_MODERATOR";

			private var confirmingLogout:Boolean = false;
			
			private const THRESHOLD_AREA_SHOW_TOOLBARS:int = 5;
			private const HIDE_TOOLBARS_EFFECT_DURATION:int = 500;
			private const HIDE_TOOLBARS_DELAY:int = 1000;

			public function getLogWindow() : LogWindow
			{
				if (logWindow == null){
					logWindow = new LogWindow();
				}
				return logWindow;
			}
      
			public function initOptions(e:Event):void {
				brandingOptions = Options.getOptions(BrandingOptions) as BrandingOptions;
				layoutOptions = Options.getOptions(LayoutOptions) as LayoutOptions;

				updateCopyrightText();
				loadBackground();

				UsersUtil.configLockSettings();
				showToolbarOpt = layoutOptions.showToolbar;
				if (!showToolbarOpt) {
					toolbarHeight = 0;
					toolbarPaddingTop = 0;
					calculateCanvasHeight();
				}
				toolbar.displayToolbar();
				
				showFooterOpt = layoutOptions.showFooter;
				var languageOptions : LanguageOptions = Options.getOptions(LanguageOptions) as LanguageOptions;
				langSelector.visible = langSelector.includeInLayout = languageOptions.userSelectionEnabled;
				
				if (!showFooterOpt) {
					footerHeight = 0;
					controlBar.visible = controlBar.includeInLayout = false;
					calculateCanvasHeight();
				}
			}
		
			private function updateCopyrightText():void {
				if (StringUtils.isEmpty(brandingOptions.copyright)) {
					copyrightText = ResourceUtil.getInstance().getString('bbb.mainshell.copyrightLabel2',[appVersion]);
				} else {
					copyrightText = String(brandingOptions.copyright).replace("{0}", appVersion);
				}
			}

			protected function initializeShell():void {	
				globalDispatcher = new Dispatcher();
				
				_hideToolbarsTimer = new Timer(HIDE_TOOLBARS_DELAY, 1);
				_hideToolbarsTimer.addEventListener(TimerEvent.TIMER, onHideToolbarsTimerComplete);

				if (stage == null) {
					addEventListener(Event.ADDED_TO_STAGE, initFullScreen);
				} else {
					initFullScreen();
				}

				systemManager.addEventListener(Event.RESIZE, handleApplicationResizeEvent);
				copyrightLabel2.addEventListener(FlexEvent.UPDATE_COMPLETE, updateCopyrightLabelDimensions);
				handleApplicationResizeEvent();
			}

			private function handleApplicationResizeEvent(e:Event = null):void {
				calculateCanvasHeight();
				updateCopyrightLabelDimensions();
			}
			
			private function setGuestPolicy(event:BBBEvent):void {
				guestPolicy = event.payload.guestPolicy;
				if(guestManagement == null) {
					LOGGER.debug("ADD Guest Event");
					guestManagement = new GuestManagement();
					guestManagement.setGuestPolicy(guestPolicy);
					guestManagement.addToSettings();
				}
			}

			private function isFullScreen():Boolean {
				return stage.displayState == StageDisplayState.FULL_SCREEN ||
						stage.displayState == StageDisplayState.FULL_SCREEN_INTERACTIVE;
			}

			private function mouseMoveHandler(e:MouseEvent):void {
				if (isFullScreen()) {
					if (e.stageY <= THRESHOLD_AREA_SHOW_TOOLBARS || e.stageY > this.height - THRESHOLD_AREA_SHOW_TOOLBARS) {
						showToolbars();
					} else if (e.stageY >= toolbar.height && e.stageY <= this.height - controlBar.height) {
						hideToolbars();
					}
				} else {
					showToolbars();
				}
			}

			private function set showToolbar(visible:Boolean):void {
				if (!showToolbarOpt) return;

				if (visible) {
					if (enlargeToolbar.isPlaying) return;
					if (shrinkToolbar.isPlaying) shrinkToolbar.end();

					enlargeToolbar.heightFrom = toolbar.height;
					if (enlargeToolbar.heightFrom != enlargeToolbar.heightTo) {
						_showToolbar = true;
						enlargeToolbar.play([toolbar]);
					}
				} else {
					if (shrinkToolbar.isPlaying) return;
					if (enlargeToolbar.isPlaying) enlargeToolbar.end();

					shrinkToolbar.heightFrom = toolbar.height;
					if (shrinkToolbar.heightFrom != shrinkToolbar.heightTo) {
						_showToolbar = true;
						shrinkToolbar.play([toolbar]);
					}
				}
			}


			private function onToolbarEffectEnd():void {
				_showToolbar = showToolbarOpt && (toolbar.height > 0);
				calculateCanvasHeight();
			}

			private function set showFooter(visible:Boolean):void {
				if (!showFooterOpt) return;

				if (visible) {
					if (enlargeControlBar.isPlaying) return;
					if (shrinkControlBar.isPlaying) shrinkControlBar.end();

					enlargeControlBar.heightFrom = controlBar.height;
					if (enlargeControlBar.heightFrom != enlargeControlBar.heightTo) {
						_showFooter = true;
						enlargeControlBar.play([controlBar]);
					}
				} else {
					if (shrinkControlBar.isPlaying) return;
					if (enlargeControlBar.isPlaying) enlargeControlBar.end();

					shrinkControlBar.heightFrom = controlBar.height;
					if (shrinkControlBar.heightFrom != shrinkControlBar.heightTo) {
						_showFooter = true;
						shrinkControlBar.play([controlBar]);
					}
				}
			}

			private function onControlBarEffectEnd():void {
				_showFooter = showFooterOpt && (controlBar.height > 0);
				calculateCanvasHeight();
			}

			private function onHideToolbarsTimerComplete(event:TimerEvent):void {
				showToolbar = showFooter = false;
			}

			private function hideToolbars():void {
				_hideToolbarsTimer.reset();
				_hideToolbarsTimer.start();
			}

			private function showToolbars():void {
				_hideToolbarsTimer.reset();
				showToolbar = showFooter = true;
			}

			private function updateCopyrightLabelDimensions(e:Event = null):void {
				var screenRect:Rectangle = systemManager.screen;

				if (spacer.width == 0) {
					copyrightLabel2.width += (screenRect.width - controlBar.width);
				} else {
					copyrightLabel2.width += Math.min(spacer.width, copyrightLabel2.measuredWidth - copyrightLabel2.width);
				}
			}
		
			protected function initFullScreen(e:Event = null):void {
				stage.addEventListener(FullScreenEvent.FULL_SCREEN, fullScreenHandler);
				stage.addEventListener(MouseEvent.MOUSE_MOVE, mouseMoveHandler);
			}
			
			private var sendStartModulesEvent:Boolean = true;
			
			// We have to calculate the canvas height manually because if you set it to a 
			// percentage you will get execution timeout errors.
			// See issue #2437 for why you shouldn't set height to 100% 
			//             https://github.com/bigbluebutton/bigbluebutton/issues/2437
			private function calculateCanvasHeight():void {
				var screenRect:Rectangle = systemManager.screen;
				mdiCanvas.height = screenRect.height - (_showFooter? footerHeight: 0) - (_showToolbar? toolbarHeight: 0);
				updateCanvasBackgroundDimensions();
			}
			
			private function handleApplicationVersionEvent(event:AppVersionEvent):void {
                var logData:Object = UsersUtil.initLogData();
                
				if (event.configLocaleVersion == true) {
					receivedConfigLocaleVer = true;
					appVersion = event.appVersion;
					localeVersion = event.localeVersion;
                                        
                    logData.localeVersion = localeVersion;
                    logData.tags = ["locale"];
                    logData.message = "Received locale version from config.xml";
                    LOGGER.info(JSON.stringify(logData));
      
					LOGGER.debug("Received locale version fron config.xml");
				} else {
					receivedResourceLocaleVer = true;

                    logData.tags = ["locale"];
                    logData.message = "Received locale version from locale file";
                    LOGGER.info(JSON.stringify(logData));
				}
				
				if (receivedConfigLocaleVer && receivedResourceLocaleVer) {
					LOGGER.debug("Comparing locale versions.");
					if (!event.suppressLocaleWarning) checkLocaleVersion(localeVersion);
					if (sendStartModulesEvent) {
            
						sendStartModulesEvent = false;
						sendStartAllModulesEvent();
					}				
				}						
			}
			
			public function sendStartAllModulesEvent():void{
				LOGGER.debug("Sending start all modules event");
				var dispatcher:Dispatcher = new Dispatcher();
				dispatcher.dispatchEvent(new ModuleLoadEvent(ModuleLoadEvent.START_ALL_MODULES));	
			}
			
			public function guestAllowed(evt:BBBEvent):void {
				progressBar.visible = true;
				if (waitWindow != null) {
					PopUpUtil.removePopUp(waitWindow);
					waitWindow = null;
				}
			}

			private function fullScreenHandler(evt:FullScreenEvent):void {
				dispState = stage.displayState + " (fullScreen=" + evt.fullScreen.toString() + ")";
				if (evt.fullScreen) {
					LOGGER.debug("Switching to full screen");
					fullscreen_icon = images.exit_full_screen;
					hideToolbars();
				} else {
					LOGGER.debug("Switching to normal screen");
					fullscreen_icon = images.full_screen;
					showToolbars();
				}
			}			

			private function closeGuestWindow(e:Event = null):void {
				if(guestWindow != null) {
					guestWindow.closeWindow();
					guestWindow = null;
				}
			}

			private function refreshGuestView(evt:RefreshGuestEvent):void {
				// do not show the guest window if the user isn't moderator or if he's waiting for acceptance
				if (!UsersUtil.amIModerator() || UsersUtil.amIWaitingForAcceptance()) {
					closeGuestWindow();
					return;
				}

				if (guestWindow == null) {
					guestWindow = PopUpUtil.createModalPopUp( mdiCanvas, GuestWindow, false) as GuestWindow;
					guestWindow.addEventListener(Event.CLOSE, closeGuestWindow);

					guestWindow.x = systemManager.screen.width - guestWindow.width - 20;
					guestWindow.y = 20;
				}
				guestWindow.refreshGuestView(evt.listOfGuests);
			}

			public function removeGuestWindow(evt:BBBEvent):void {
				if (guestWindow != null) {
					guestWindow.remove(evt.payload.userId);
				}
			}

			private function closeWaitWindow(e:BBBEvent):void {
				if(waitWindow != null && e.payload.type == "BIGBLUEBUTTON_CONNECTION") {
					LOGGER.debug("closeWaitWindow");
					waitWindow.removeWindow();
				}
			}

			private function openWaitWindow(evt:BBBEvent):void {
				progressBar.visible = false;
				waitWindow = PopUpUtil.createModalPopUp( mdiCanvas, WaitingWindow, false) as WaitingWindow;

				// Calculate position of TitleWindow in Application's coordinates.
				waitWindow.x = (systemManager.screen.width - waitWindow.width) / 2;
				waitWindow.y = (systemManager.screen.height - waitWindow.height) / 2;
			}
			
			private function openLogWindow():void {
				mdiCanvas.windowManager.add(getLogWindow());
				mdiCanvas.windowManager.absPos(logWindow, 50, 50);
				logWindow.width = mdiCanvas.width - 100;
				logWindow.height = mdiCanvas.height - 100;
			}
			
			public function openShortcutHelpWindow(e:Event = null):void{
				if (scWindow == null) {
					scWindow = new ShortcutHelpWindow();
					scWindow.width = 300;
					scWindow.height = 300;
				}
				
				if (scWindow.minimized)
					scWindow.restore();
				
				if (scWindow.maximized)
					scWindow.restore();
				
				mdiCanvas.windowManager.add(scWindow);
				mdiCanvas.windowManager.absPos(scWindow, mdiCanvas.width/2 - 150, mdiCanvas.height/2 - 150);
				
				scWindow.focusHead();
			}
			
			private function toggleFullScreen():void{
				LOGGER.debug("Toggling fullscreen, current mode: " + stage.displayState);
	   			try {
					switch (stage.displayState) {
						case StageDisplayState.FULL_SCREEN:
						case StageDisplayState.FULL_SCREEN_INTERACTIVE:
							LOGGER.debug("Full screen mode, switching to normal screen mode");
							// If already in full screen mode, switch to normal mode.
							stage.displayState = StageDisplayState.NORMAL;
							break;
						default:
							LOGGER.debug("Normal screen mode, switching to full screen mode");
							// If not in full screen mode, switch to full screen mode.
							stage.displayState = StageDisplayState.FULL_SCREEN_INTERACTIVE;
							break;
					}
				} catch (err:SecurityError) {
					// ignore
				}
	   		}	
	   		
	   		private function handleOpenWindowEvent(event:OpenWindowEvent):void {
				// this is a terrible place for these checks because this function runs 4 times on startup
				if (BBB.initConnectionManager().isTunnelling) {
					isTunneling = true;
					globalDispatcher.dispatchEvent(new ClientStatusEvent(ClientStatusEvent.WARNING_MESSAGE_EVENT,
						ResourceUtil.getInstance().getString("bbb.clientstatus.tunneling.title"),
						ResourceUtil.getInstance().getString("bbb.clientstatus.tunneling.message"),
						'bbb.clientstatus.tunneling'));
				}
				versionCheck();
				
				var window:IBbbModuleWindow = event.window;
	   			mdiCanvas.addWindow(window);
	   		}
	   		
	   		private function handleCloseWindowEvent(event:CloseWindowEvent):void {
	   			var window:IBbbModuleWindow = event.window;
				  mdiCanvas.removeWindow(window);
	   		}
	   		
			private function versionCheck():void {
				var browserOptions : BrowserVersionsOptions = Options.getOptions(BrowserVersionsOptions) as BrowserVersionsOptions;
				if (!StringUtils.isEmpty(browserOptions.chrome) && !StringUtils.isEmpty(browserOptions.firefox) && !StringUtils.isEmpty(browserOptions.flash)) {
					//find browser version
					var browserVersion:Array = ExternalInterface.call("determineBrowser");
					//check browser version
					if ((browserVersion[0].toString().toLowerCase() == "chrome" && browserVersion[1] < browserOptions.chrome) || browserVersion[0].toString().toLowerCase() == "firefox" && browserVersion[1] < browserOptions.firefox) {
						globalDispatcher.dispatchEvent(new ClientStatusEvent(ClientStatusEvent.WARNING_MESSAGE_EVENT, 
							ResourceUtil.getInstance().getString("bbb.clientstatus.browser.title"), 
							ResourceUtil.getInstance().getString("bbb.clientstatus.browser.message", [browserVersion[0]+" "+browserVersion[1]]),
							'bbb.clientstatus.browser.message'));
					}
					
					//find flash version
					var flashVersion:Object = getFlashVersion(); 
					//check flash version
					if ((flashVersion.os == 'LNX' && browserVersion[0].toString().toLowerCase() != "chrome" && flashVersion.major < 11) || 
						((flashVersion.os != 'LNX' || browserVersion[0].toString().toLowerCase() == "chrome") && flashVersion.major < browserOptions.flash)) {
						globalDispatcher.dispatchEvent(new ClientStatusEvent(ClientStatusEvent.WARNING_MESSAGE_EVENT, 
							ResourceUtil.getInstance().getString("bbb.clientstatus.flash.title"), 
							ResourceUtil.getInstance().getString("bbb.clientstatus.flash.message", [flashVersion.major+"."+flashVersion.minor+"."+flashVersion.build]),
							'bbb.clientstatus.flash.message'));
					}
				}

				//Alert.show(LOG + "version check- os: "+flashVersion.os+" major:"+flashVersion.major+" minor:"+flashVersion.minor+" build:"+flashVersion.build+" browser:"+browserVersion[0]+" version:"+browserVersion[1]);
				
				//find java version
				//check flash version
			}
			
			//function found here, http://www.mediacollege.com/adobe/flash/actionscript/3/player-version.html
			private function getFlashVersion():Object {
				var flashVersion:Object = new Object();
				var versionNumber:String = Capabilities.version;// Get the whole version string
				var versionArray:Array = versionNumber.split(",");// Split it up
				var osPlusVersion:Array = versionArray[0].split(" ");// The main version contains the OS (e.g. WIN), so we split that off as well.
				// Populate the version object (the OS is a string, others are numbers):
				flashVersion["os"] = osPlusVersion[0];
				flashVersion["major"] = parseInt(osPlusVersion[1]);
				flashVersion["minor"] = parseInt(versionArray[1]);
				flashVersion["build"] = parseInt(versionArray[2]);
				return flashVersion;
			}
			
	   		private function resetLayout():void{
	   			mdiCanvas.resetWindowLayout();
	   		}
	   		
	   		private function addComponentToCanvas(e:AddUIComponentToMainCanvas):void{
	   			mdiCanvas.addChild(e.component);
	   		}
	   		
	   		public function checkLocaleVersion(localeVersion:String):void {	   			
	   			Alert.okLabel ="OK";
				var version:String = "old-locales";
				version = ResourceUtil.getInstance().getString('bbb.mainshell.locale.version');
                
                var logData:Object = UsersUtil.initLogData();
                logData.configVersion = localeVersion;
                logData.localeVersion = version;
                logData.locale = ResourceUtil.getInstance().getCurrentLanguageCode();
                logData.tags = ["locale"];
                logData.message = "Loaded locale.";
                LOGGER.info(JSON.stringify(logData));
                    
				if ((version == "old-locales") || (version == "") || (version == null)) {
					wrongLocaleVersion();
				} else {
					if (version != localeVersion) wrongLocaleVersion();
				}	   			
	   		}

			private function handleInactivityWarningEvent(e:BBBEvent):void {
				var inactivityWarning:InactivityWarningWindow = PopUpUtil.createModalPopUp(mdiCanvas, InactivityWarningWindow, true) as InactivityWarningWindow;
				inactivityWarning.duration = e.payload.duration;
			}

            private function handleFlashMicSettingsEvent(event:FlashMicSettingsEvent):void {
                /**
                 * There is a bug in Flex SDK 4.14 where the screen stays blurry if a
                 * pop-up is opened from another pop-up. I delayed the second open to
                 * avoid this case. - Chad
                 */
                this.callLater(function():void {
                    var micSettings:FlashMicSettings = PopUpUtil.createModalPopUp(mdiCanvas, FlashMicSettings, false) as FlashMicSettings;
                    if (micSettings) {
                        micSettings.addEventListener(FlexEvent.CREATION_COMPLETE, function(e:Event):void {
                            var point1:Point = new Point();
                            // Calculate position of TitleWindow in Application's coordinates. 
                            point1.x = width / 2;
                            point1.y = height / 2;
                            micSettings.x = point1.x - (micSettings.width / 2);
                            micSettings.y = point1.y - (micSettings.height / 2);
                        });
                    }
                });
            }

            private function openVideoPreviewWindow(event:BBBEvent):void {
                var camSettings:CameraDisplaySettings = PopUpUtil.createModalPopUp(mdiCanvas, CameraDisplaySettings, true) as CameraDisplaySettings;
                if (camSettings) {
                    camSettings.defaultCamera = event.payload.defaultCamera;
                    camSettings.camerasArray = event.payload.camerasArray;
                    camSettings.publishInClient = event.payload.publishInClient;
                    camSettings.chromePermissionDenied = event.payload.chromePermissionDenied;
					camSettings.updateCameraList();

                    var point1:Point = new Point();
                    // Calculate position of TitleWindow in Application's coordinates. 
                    point1.x = width / 2;
                    point1.y = height / 2;
                    camSettings.x = point1.x - (camSettings.width / 2);
                    camSettings.y = point1.y - (camSettings.height / 2);
                }
            }

            private function wrongLocaleVersion():void {
                var localeWindow:OldLocaleWarnWindow = PopUpUtil.createNonModelPopUp(mdiCanvas, OldLocaleWarnWindow, false) as OldLocaleWarnWindow;
                if (localeWindow) {
                    var point1:Point = new Point();
                    // Calculate position of TitleWindow in Application's coordinates. 
                    point1.x = width / 2;
                    point1.y = height / 2;
                    localeWindow.x = point1.x - (localeWindow.width / 2);
                    localeWindow.y = point1.y - (localeWindow.height / 2);
                }
            }

            private function handleShowAudioSelectionWindowEvent(event:AudioSelectionWindowEvent):void {
                PopUpUtil.createModalPopUp(mdiCanvas, AudioSelectionWindow, true);
            }

            private function handleWebRTCMediaRequestEvent(event:WebRTCMediaEvent):void {
                var options:PhoneOptions = new PhoneOptions();
                if (!options.showMicrophoneHint) return;
                var browser:String = ExternalInterface.call("determineBrowser")[0];
                if (browser == "Firefox") {
                    var ffBrowser:FirefoxMicPermissionImage = PopUpUtil.createModalPopUp(mdiCanvas, FirefoxMicPermissionImage, false) as FirefoxMicPermissionImage;
                    if (ffBrowser) {
                        ffBrowser.addEventListener(FlexEvent.CREATION_COMPLETE, function(e:Event):void {
                            ffBrowser.x = 100;
                            ffBrowser.y = 150;
                        });
                    }
                } else if (browser == "Chrome") {
                    var chromeBrowser:ChromeMicPermissionImage = PopUpUtil.createModalPopUp(mdiCanvas, ChromeMicPermissionImage, false) as ChromeMicPermissionImage;
                    if (chromeBrowser) {
                        chromeBrowser.addEventListener(FlexEvent.CREATION_COMPLETE, function(e:Event):void {
                            chromeBrowser.x = 20;
                            chromeBrowser.y = 130;
                        });
                    }
                }
            }

            private function handleWebRTCEchoTestConnectingEvent(event:WebRTCEchoTestEvent):void {
                var webRTCEchoTest:WebRTCEchoTest = PopUpUtil.createModalPopUp(mdiCanvas, WebRTCEchoTest, false) as WebRTCEchoTest;
                if (webRTCEchoTest) {
                    webRTCEchoTest.addEventListener(FlexEvent.CREATION_COMPLETE, function(e:Event):void {
                        var point1:Point = new Point();
                        // Calculate position of TitleWindow in Application's coordinates. 
                        point1.x = width / 2;
                        point1.y = height / 2;
                        webRTCEchoTest.x = point1.x - (webRTCEchoTest.width / 2);
                        webRTCEchoTest.y = point1.y - (webRTCEchoTest.height / 2);
                    });
                }
            }

            private function handleShareCameraRequestEvent(event:ShareCameraRequestEvent):void {
                if (ExternalInterface.call("determineBrowser")[0] == "Chrome") {
                    var chromeWebcmPermissionImg : ChromeWebcamPermissionImage = PopUpUtil.createModalPopUp(mdiCanvas, ChromeWebcamPermissionImage, false) as ChromeWebcamPermissionImage;
                    if (chromeWebcmPermissionImg) {
                        chromeWebcmPermissionImg.addEventListener(FlexEvent.CREATION_COMPLETE, function(e:Event):void {
                            var point1:Point = new Point();
                            // Calculate position of TitleWindow in Application's coordinates.
                            point1.x = width / 2;
                            point1.y = height / 2;
                            if (Capabilities.os.indexOf("Mac") >= 0) {
                                chromeWebcmPermissionImg.x = (mdiCanvas.width - chromeWebcmPermissionImg.width);
                            } else {
                                chromeWebcmPermissionImg.x = 20;
                            }
                            chromeWebcmPermissionImg.y = 20;
                        });
                    }
                }
            }

            private function handleMeetingNotFoundEvent(e:MeetingNotFoundEvent):void {
                showlogoutWindow(ResourceUtil.getInstance().getString('bbb.mainshell.meetingNotFound'));
            }

            private function showlogoutWindow(reason:String):void {
                if (layoutOptions != null && layoutOptions.showLogoutWindow) {
                    if (UsersUtil.iAskedToLogout()) {
                        handleExitApplicationEvent();
                        return;
                    }
                    var logoutWindow:LoggedOutWindow = PopUpUtil.createModalPopUp(mdiCanvas, LoggedOutWindow, false) as LoggedOutWindow;
                    if (logoutWindow) {
                        var point1:Point = new Point();
                        // Calculate position of TitleWindow in Application's coordinates. 
                        point1.x = width / 2;
                        point1.y = height / 2;
                        logoutWindow.x = point1.x - (logoutWindow.width / 2);
                        logoutWindow.y = point1.y - (logoutWindow.height / 2);

                        logoutWindow.setReason(reason);
                        mdiCanvas.removeAllPopUps();
                        removeToolBars();
                    }
                } else {
                    mdiCanvas.removeAllPopUps();
                    removeToolBars();
                    var pageHost:String = FlexGlobals.topLevelApplication.url.split("/")[0];
                    var pageURL:String = FlexGlobals.topLevelApplication.url.split("/")[2];
                    LOGGER.debug("SingOut to [{0}//{1}/bigbluebutton/api/signOut]", [pageHost, pageURL]);
                    var request:URLRequest = new URLRequest(pageHost + "//" + pageURL + "/bigbluebutton/api/signOut");
                    var urlLoader:URLLoader = new URLLoader();
                    urlLoader.addEventListener(Event.COMPLETE, handleLogoutComplete);
                    urlLoader.addEventListener(IOErrorEvent.IO_ERROR, handleLogoutError);
                    urlLoader.load(request);
                }
            }

            /**
             * Removes toolbars from the display list.
             * Used only when the user completely logged out.
             */
            private function removeToolBars():void {
                this.removeChild(toolbar);
                this.removeChild(controlBar);
            }

            private function handleLogout(e:ConnectionFailedEvent):void {
                if (e is ConnectionFailedEvent) {
                    showlogoutWindow((e as ConnectionFailedEvent).type);
                } else
                    showlogoutWindow("You have logged out of the conference");
            }

            private function handleExitApplicationEvent(e:ExitApplicationEvent = null):void {
                if (! UsersUtil.isBreakout()) {
                    navigateToURL(new URLRequest(BBB.getLogoutURL()), "_self");
                } else {
                    ExternalInterface.call("window.close");
                }
            }

			private function redirectToLogoutUrl ():void {
        		var logoutURL:String = BBB.getLogoutURL();
				var request:URLRequest = new URLRequest(logoutURL);
				LOGGER.debug("Logging out to: {0}", [logoutURL]);
				navigateToURL(request, '_self');
			}
			

			private function handleLogoutError(e:Event):void {
				LOGGER.debug("Call to signOut URL failed.");
				redirectToLogoutUrl();
			}
			
			private function handleLogoutComplete(e:Event):void {	
				LOGGER.debug("Call to signOut URL succeeded.");
				redirectToLogoutUrl();
			}
			
			private function attemptReconnect(e:ConnectionFailedEvent):void{
				handleLogout(e);
			}
			
			private function handleAddToolbarComponent(event:ToolbarButtonEvent):void {
				if (event.location == ToolbarButtonEvent.BOTTOM_TOOLBAR) {
					(event.button as UIComponent).tabIndex = tabIndexer.startIndex + addedBtns.numChildren + 10;
					addedBtns.addChild(event.button as UIComponent);
					//realignButtons();
				}
			}
			
			private function handleWebRTCCallStartedEvent(e:WebRTCCallEvent):void {
				lblWebRTC.visible = lblWebRTC.includeInLayout = true;
			}
			
			private function handleWebRTCCallEndedEvent(e:WebRTCCallEvent):void {
				lblWebRTC.visible = lblWebRTC.includeInLayout = false;
			}

            private function handleInvalidAuthToken(event:InvalidAuthTokenEvent):void {
                showlogoutWindow(ResourceUtil.getInstance().getString('bbb.mainshell.invalidAuthToken'));
                globalDispatcher.dispatchEvent(new BBBEvent(BBBEvent.CANCEL_RECONNECTION_EVENT));
            }

			private function handleRemoveToolbarComponent(event:ToolbarButtonEvent):void {
				if (addedBtns.contains(event.button as UIComponent))
					addedBtns.removeChild(event.button as UIComponent);
			}

			private var networkStatsWindow:NetworkStatsWindow = null;

			private function openNetworkStatsWindow(e:Event = null):void {
				if (networkStatsWindow == null) {
					networkStatsWindow = new NetworkStatsWindow();
				}
				
				mdiCanvas.windowManager.add(networkStatsWindow);
				mdiCanvas.windowManager.absPos(networkStatsWindow, mdiCanvas.width - networkStatsWindow.width, 0);
				mdiCanvas.windowManager.bringToFront(networkStatsWindow);
			}

			private function updateToolbarHeight():void {
				if (toolbarHeight != 0) {
					toolbarHeight = Math.max(DEFAULT_TOOLBAR_HEIGHT, toolbar.logo.height + 10);
				}
				calculateCanvasHeight();
			}

			private function updateCanvasBackgroundDimensions():void {
				if (canvasBackground == null) {
					return;
			}
			
				var canvasAspectRatio:Number = mdiCanvas.width / mdiCanvas.height;

				// this is the case when the image is a banner that should be positioned in the top, and
				// be shown entirely
				if (canvasBackgroundAspectRatio > 2 || canvasAspectRatio >= canvasBackgroundAspectRatio) {
					canvasBackground.width = mdiCanvas.width;
					canvasBackground.height = Math.ceil(mdiCanvas.width / canvasBackgroundAspectRatio);
				} else {
					canvasBackground.height = mdiCanvas.height;
					canvasBackground.width = Math.ceil(mdiCanvas.height * canvasBackgroundAspectRatio);
				}
			}

			private var canvasBackground:DisplayObject = null;
			private var canvasBackgroundAspectRatio:Number = NaN;

			private function loadBackground():void {
				var backgroundCandidate:String = brandingOptions.background;
				if (!StringUtils.isEmpty(backgroundCandidate)) {
					var imageLoader:ImageLoader = new ImageLoader();
					imageLoader.load(backgroundCandidate, function(obj:DisplayObject, width:Number, height:Number):void {
						canvasBackground = obj;
						canvasBackgroundAspectRatio = width / height;
						backgroundPlaceHolder.rawChildren.addChild(canvasBackground);
						backgroundPlaceHolder.invalidateDisplayList();
						updateCanvasBackgroundDimensions();
					});
				}
			}

            private function openLockSettingsWindow(event:LockControlEvent):void {
                var lsv:LockSettingsVO = UsersUtil.getLockSettings();

                var popUp:IFlexDisplayObject = PopUpUtil.createModalPopUp(mdiCanvas, LockSettings, false);
                if (popUp) {
                    var ls:LockSettings = LockSettings(popUp);
                    ls.disableCam = lsv.getDisableCam();
                    ls.disableMic = lsv.getDisableMic();
                    ls.disablePrivChat = lsv.getDisablePrivateChat();
                    ls.disablePubChat = lsv.getDisablePublicChat();
                    ls.lockedLayout = lsv.getLockedLayout();
                    ls.lockOnJoin = lsv.getLockOnJoin();
                    ls.lockOnJoinConfigurable = lsv.getLockOnJoinConfigurable();

                    var point1:Point = new Point();
                    point1.x = width / 2;
                    point1.y = height / 2;
                    ls.x = point1.x - (ls.width / 2);
                    ls.y = point1.y - (ls.height / 2);
                }
            }

            private function openBreakoutRoomsWindow(e:BreakoutRoomEvent):void {
                var popUp:IFlexDisplayObject = PopUpUtil.createModalPopUp(mdiCanvas, BreakoutRoomSettings, true);
                if (popUp != null) {
                    BreakoutRoomSettings(popUp).initCreateBreakoutRooms(e.joinMode, e.record);
                }
            }
			
			private function onFooterLinkClicked(e:TextEvent):void{
				if (ExternalInterface.available) {
					ExternalInterface.call("chatLinkClicked", e.text);
				}
			}

            private function onLayoutChanged(e:SwitchedLayoutEvent):void {
                if (e.layoutID != "bbb.layout.name.videochat") {
                    this.styleName = "defaultShellStyle";
					controlBar.styleName = "defaultControlBarStyle";
                } else {
					this.styleName = "darkShellStyle";
					controlBar.styleName = "darkControlBarStyle";
                }
            }

		]]>
	</fx:Script>

	<fx:Declarations>
		<mx:Resize id="shrinkControlBar" duration="{HIDE_TOOLBARS_EFFECT_DURATION}" heightFrom="{footerHeight}" heightTo="0" effectEnd="onControlBarEffectEnd()" />
		<mx:Resize id="enlargeControlBar" duration="{HIDE_TOOLBARS_EFFECT_DURATION}" heightFrom="0" heightTo="{footerHeight}" effectEnd="onControlBarEffectEnd()" />
		<mx:Resize id="shrinkToolbar" target="{toolbar}" duration="{HIDE_TOOLBARS_EFFECT_DURATION}" heightFrom="{toolbarHeight}" heightTo="0" effectEnd="onToolbarEffectEnd()" />
		<mx:Resize id="enlargeToolbar" duration="{HIDE_TOOLBARS_EFFECT_DURATION}" heightFrom="0" heightTo="{toolbarHeight}" effectEnd="onToolbarEffectEnd()" />
	</fx:Declarations>
	
	<fx:Declarations>
		<common:TabIndexer id="tabIndexer" startIndex="100000"
						   tabIndices="{[warningBtn, langSelector, logBtn]}"/>
	</fx:Declarations>

	<views:MainToolbar id="toolbar" 
					   dock="true" 
					   width="100%" 
					   height="{toolbarHeight}" 
					   visible="{_showToolbar}"
					   includeInLayout="{showToolbarOpt}"
					   verticalAlign="middle" 
					   toolbarOptions="{layoutOptions}" 
					   paddingTop="{toolbarPaddingTop}"
					   updateComplete="updateToolbarHeight()"
					   paddingBottom="0" />
					   
	<views:MainCanvas id="mdiCanvas"
					  horizontalScrollPolicy="off"
					  verticalScrollPolicy="off"
					  styleName="mdiCanvasStyle"
					  effectsLib="{flexlib.mdi.effects.effectsLib.MDIVistaEffects}" 
					  width="100%" >
		<mx:Canvas id="backgroundPlaceHolder" width="100%" height="100%" />
		<views:LoadingBar id="progressBar" horizontalCenter="0" verticalCenter="0" width="280" height="15"/>
		<views:BrandingLogo x="{this.width - 300}" y="{this.height - 300}" />
	</views:MainCanvas>	
	
	<mx:ControlBar width="100%" height="{footerHeight}" id="controlBar" paddingTop="2" paddingBottom="2" verticalAlign="middle" visible="{_showFooter}" includeInLayout="{showFooterOpt}" >
		<mx:Label
				htmlText="{copyrightText}"
				link="onFooterLinkClicked(event)"
				id="copyrightLabel2" truncateToFit="true"
				selectable="true" paddingRight="10"/>
		<mx:Spacer width="100%" id="spacer" />
		<mx:Label 
				id="lblWebRTC"
				text="{'[ '+ResourceUtil.getInstance().getString('bbb.mainshell.notification.webrtc')+' ]'}"
				visible="false"
				includeInLayout="false" />
		<mx:Label
				text="{'['+ResourceUtil.getInstance().getString('bbb.mainshell.notification.tunnelling')+']'}"
				id="isTunnellingLbl"
				visible="{isTunneling}"
				includeInLayout="{isTunneling}" />
		<views:WarningButton id="warningBtn" 
				width="23"
				height="20"/>
		<views:LanguageSelector id="langSelector" 
				visible="false" 
				height="20"
				width="180"
				accessibilityName="{ResourceUtil.getInstance().getString('bbb.mainToolbar.langSelector')}"/>
		<mx:Button
				width="20"
				height="20"
				visible="{layoutOptions.showLogButton}"
				includeInLayout="{layoutOptions.showLogButton}"
				toolTip="{ResourceUtil.getInstance().getString('bbb.mainshell.logBtn.toolTip')}"
				id="logBtn"
				icon="{logs_icon}"
				click="openLogWindow()"/>
		<mx:HBox id="addedBtns" />
		<mx:Button width="20" height="20" toolTip="{ResourceUtil.getInstance().getString('bbb.mainshell.fullscreenBtn.toolTip')}" id="fullScreen" icon="{fullscreen_icon}" click="toggleFullScreen()" />
	</mx:ControlBar>
</mx:VBox>
